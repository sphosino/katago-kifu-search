#packopt name "KataGo棋譜検索"
#packopt icon "original.ico"
screen 0, 730,580
#include "go_game_manager.hsp"
#include "modules/katago_sqr.hsp"
#include "modules/keyfps.hsp"
#include "GobanPatternManager.hsp"
#include "Search_pattern.hsp"
#include "shuffle_notepad.hsp"
#include "modules/grid_system/area_grid_system.hsp"
#include "modules/grid_system/g_button.hsp"
#include "modules/grid_system/g_chkbox.hsp"
#include "modules/grid_system/g_listbox.hsp"
#include "modules/grid_system/g_slider.hsp"
#include "hspext.as"

#ifdef _debug
dirname = "C:\\Users\\owner\\Desktop\\hsp\\棋譜"
#else
dirname = dir_exe + "/棋譜"
#endif

chdir dirname

randomize

header_size = 60
pos,header_size
setfps 40

auto_interval = 1000

v = "" //矩形選択用

jidou = 0
着手番号表示 = 0

set_treeview 50,50
tree_direction = 6

prenode = -1


topnode = -1

//エリア作成
root = make_area(1,2)
	header_area = make_area()
	body_area = make_area(2,1)
		goban_area = make_area()
		sidebar_area = make_area(1,3)
			sidebar_kifulist = make_area()
			sidebar_kifulist_match = make_area()
			sidebar_buttons = make_area(2,4)
				button_loadfile = make_area()
				button_getsgf = make_area()
				button_clearboard = make_area()
				button_search = make_area()
				chkbox_tyakusyu = make_area()
				chkbox_colorflip = make_area()
				chkbox_autoplay = make_area()
				slider = make_area()
		
//親子関係構築
push_grid_child root , header_area
push_grid_child root , body_area

push_grid_child body_area, sidebar_area
push_grid_child body_area, goban_area

push_grid_child sidebar_area, sidebar_buttons
push_grid_child sidebar_area, sidebar_kifulist
push_grid_child sidebar_area, sidebar_kifulist_match

push_grid_child sidebar_buttons, button_loadfile
push_grid_child sidebar_buttons, button_getsgf
push_grid_child sidebar_buttons, button_clearboard
push_grid_child sidebar_buttons, button_search
push_grid_child sidebar_buttons, chkbox_tyakusyu
push_grid_child sidebar_buttons, chkbox_colorflip
push_grid_child sidebar_buttons, chkbox_autoplay
push_grid_child sidebar_buttons, slider

exist "area.var"
if strsize >= 0{
	save_load_manager_load
}
alpha@ = 50
//実体作成
newmod g_buttons, grid_button, "最新棋譜を取得", *loadfile, button_loadfile
newmod g_buttons, grid_button, "SGFコピー"   , *getsgf  , button_getsgf
newmod g_buttons, grid_button, "碁盤クリア"  , *clearboard, button_clearboard
newmod g_buttons, grid_button, "検索"          , *create_pattern, button_search
newmod g_chkbox, grid_checkbox, mode           , "着手" , chkbox_tyakusyu
newmod g_chkbox, grid_checkbox, enableColorFlip,"色反転", chkbox_colorflip
newmod g_chkbox, grid_checkbox, jidou          ,"自動再生",chkbox_autoplay
newmod g_slider, grid_slider, alpha@, slider, 0, 255
newmod kifulist, grid_listbox, sidebar_kifulist
newmod kifulist, grid_listbox, sidebar_kifulist_match: matchlist = stat
newmod board, goban, 19,19,26,26, goban_area

newmod t, treebox


//矩形範囲指定用
rn = 0
dim rect, 4
dim rect_stack, 4

//探索の深さ
max_depth = 50

//色反転を含めるか
enableColorFlip = 1

foreach kifulist
	set_listbox_item_ysize kifulist.cnt, 20
loop
gosub*load_kifu_folder
	
gosub*loadsgf

pretime = keyfpstime

*main
	manage_keyfps

	nownode = get_current_node(t)
	
	getBoardCoordinates board, point

	in_board = 0
	if 0 <= point && point < 19{
		if 0 <= point.1 && point < 19{
			in_board = 1
		}
	}
	if debugmode == 0{
	//マウス操作
	if _getkey(2) == 1{ //右クリック
		if treebox_onmouseid(t) >= 0{
			set_current_node t, treebox_onmouseid(t)
			Treebox_toggle_openflag t, treebox_onmouseid(t)
		}
		if mode == 0{//矩形描画モード
			if rn > 0{
				rn--
				modify_coordinates_by_rect v, rect_stack(0,rn), rect_stack(1, rn), rect_stack(2, rn), rect_stack(3, rn), 1
			}
		}
	}
	if _getkey(1) == 1{ //左クリック

		inlistbox = 0

		foreach kifulist
			itemid = listbox_on_id(kifulist.cnt,mousex, mousey)
			if itemid >= 0{	
				if get_root_tree_node(t) >= 0{
					del_treenode(get_root_tree_node(t))
				}
				sdim v
				rn = 0

				check_item kifulist.cnt,itemid, 1 //アイテムにチェックを付ける
				
				items = get_listbox_item_list(kifulist.cnt)
				notesel items
				noteget s, itemid
				noteunsel
				topnode = change_sgf(s, t, board)
				
				
				if get_listbox_item_data(kifulist.cnt,itemid){ //アイテムデータが設定されていたら、それは検索結果。
					nstat = stat //検索結果のSGFノード
					//それが格納されているツリーIDを探してカレントノードに設定
					set_current_node t, search_tree_by_item_id(get_root_tree_node(t), nstat)
					//碁盤を同期
					sync_treebox_board t, board
				}
				set_treeview_position
				inlistbox++
			}

			if itemid == -3{
				set_current_scroll_index_by_bar_position kifulist.cnt, mousey
			}
		loop
		
		if inlistbox == 0{
			dragging_flag = 1
			get_treeview_position tmp
			dragstartx = mousex - tmp
			dragstarty = mousey - tmp.1
	
	
			if mode = 0{//矩形描画モード
				if in_board{
					rect = point,point.1,point,point.1
				}else{
					rect = -1
				}
			}else{
			
				if treebox_onmouseid(t) >=0{

					clicked_node = stat

					logmes "select_TreeID== "+clicked_node + " ( " + get_tree_item_id(clicked_node) +" ) "　 + length(SGF_NODE@global)

					set_current_node t, clicked_node 			
					sync_treebox_board t, board 


				}else{
					if in_board{
						着手 board, point, point.1
						
						if stat{　//着手成功
							dragging_flag = 0
							
							if nownode = -1{ //棋譜未選択状態なら

								make_tree_topnode t
								nownode = stat
								
							}
							
							now_sgfnode = get_tree_item_id(nownode)
							logmes now_sgfnode
							

							//指定した座標のノードを作成、すでにあればそれを返す
							new = make_SGFnode(now_sgfnode, point, get_turn_SGFstr(逆手番(get_turn(board))))

							//現在のツリーノード直下にそのSGFノードがあるか？あれば、そのノードを取得
							new_treeID = serch_tree_child_by_item_ID(nownode, new)
							
							if new_treeID == -1{
								//新たにツリーノードを作る
								new_treeID = make_treenode(nownode, new)
								if now_sgfnode == -1: topnode = new_tree_ID
							}
							set_current_node t, new_treeID
						}
					}
				}
			}
		}
	}

	if dragging_flag{
		
		if mode = 0{
			rect(2) = limit(point(0),0,18)
			rect(3) = limit(point(1),0,18)
		}else{
			set_treeview_position mousex - dragstartx, mousey - dragstarty
		}
		
		if _getkey(1) == -1{//マウスが離されたとき
			
			dragging_flag = 0
			if mode = 0 && rect(0) >= 0{
				modify_coordinates_by_rect v, rect(0), rect(1), rect(2), rect(3)
				rect_stack(0,rn) = rect(0), rect(1), rect(2), rect(3)
				rn++
				dim rect,4
			}
		}
	}

	//マウスホイール
	mw = mousew

	in_kifulist == 0
	foreach kifulist
		if listbox_on_id(kifulist.cnt,mousex, mousey) >= 0{
			in_kifulist++
			if mw < 0: move_scroll_index kifulist.cnt,1
			if mw > 0: move_scroll_index kifulist.cnt,-1
		}
	loop
	if in_kifulist == 0{
		if mw < 0 : gosub*move_next
		if mw > 0 : gosub*move_back
	}

	//キー操作
	if _getkey(17) >= 1{ //CTRL
		if _getkey('C') == 1 { //CTRL + C
			gosub*getsgf
		}
		if _getkey('V') == 1 { //CTRL + V
			gosub*loadsgf
		}
	}

	//自動再生
	if jidou & pretime + auto_interval <= keyfpstime{
		
		gosub *move_next
	}
	
	if prenode != nownode{
		prenode = nownode

		
		if nownode >= 0{
			sync_treebox_board t, board
			tmp = get_property_data(get_tree_item_id(nownode), "C")
			split tmp," ", c_list
		}
	}
	}


		a = double(c_list) * 255
		color a,a,a
		boxf 0, header_size , ginfo_winx,ginfo_winy
		
		碁盤表示 board , 着手番号表示
		draw_set_list_on_board board, v
		set_tree_direction tree_direction
		treebox_draw t
		foreach kifulist
			my_listbox_draw kifulist.cnt
		loop
		foreach g_chkbox
			draw_g_chkbox g_chkbox.cnt
		loop
		foreach g_buttons
			draw_g_button g_buttons.cnt
		loop
		foreach g_slider
			slider_manager g_slider.cnt
		loop
		if dragging_flag && rect >= 0:碁盤に矩形データを表示 board, rect

		if getdbl_ms(17,200): debugmode^1
		draw_all_rect root, debugmode
		title""+alpha@
		
	redraw: redraw 2

	goto*main
*clearboard
	clear_board board
	treebox_clear t
	topnode = stat
	return
*getsgf
	get_node_sgf topnode, a
	clipset a
	return
*loadsgf
	sdim s, $FFFF
	clipget s, varsize(s)
	if load_sgf(s,-1) < 0: return
	topnode = load_sgf_set_board(t, stat, board)
	return

*create_pattern
	logmes "検索開始"
	time = keyfpstime
	initialize_rects v,r, stones
	
	repeat load_stones_from_board(board, n)
		add_stone_pattern r, stones, n(0,cnt), n(1,cnt), n(2,cnt)
	loop

	create_pattern_with_rects_and_stones r,stones,p

	logmes patterns_to_notepad(p)


	result_num = search(get_name_all_items(kifulist), p, search_result, max_depth, enableColorFlip)


	search_result_sgflist = ""
	
	repeat result_num
		search_result_sgflist += search_result(0, cnt) + "\n"
	loop

	//logmes search_result_sgflist

	del_item_all kifulist.matchlist
	add_item kifulist.matchlist, search_result_sgflist
	
	repeat result_num
		add_item_data kifulist.matchlist, cnt, search_result(1,cnt)
	loop

	logmes "検索終了 -> " +(keyfpstime - time) + "ms  " + result_num + "件の検索結果"
	
	return
	
*loadfile
	
	棋譜取得 dirname, 20 // katagoホームページから最新棋譜をダウンロード
	notesel files

	dirlist files,"*.sgf"
	add_item kifulist, files

	notesel files
	sortnote files,1
	noteget s,0

	topnode = change_sgf(s,t,board)

	gosub*load_kifu_folder
	
	return

*move_next
	if nownode < 0: return

	set_current_node t, get_tree_root_child(nownode)
	if stat: treeview移動 1
	pretime = keyfpstime

	return
	
*move_back
	if nownode < 0: return
	if get_tree_parent(nownode) != -1{
		set_current_node t, get_tree_parent(nownode)
		pretime = keyfpstime
		treeview移動 -1
	}
	return
*load_kifu_folder
	chdir dirname
	del_item_all kifulist
	dirlist files,"*.sgf"
	add_item kifulist, shuffle_notepad(files)

	return
#deffunc treeview移動 int ddd
	get_treeview_node_size ns 
	if (get_tree_direction() & 1){
		move_treeview_position , ns.1 * -(((get_tree_direction() & 2) != 0) * 2 - 1) * ddd
	}else{ 
		move_treeview_position   ns   * -(((get_tree_direction() & 2) != 0) * 2 - 1) * ddd
	}
	return