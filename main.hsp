#packopt name "KataGo棋譜検索"
#include "modules/group.hsp"
#include "modules/keyfps.hsp"
#include "modules/sgf.hsp"
#include "GobanPatternManager.hsp"
#include "modules/katago_sqr.hsp"
#include "modules/set.hsp"
#include "Search_pattern.hsp"
#include "Search_pattern2.hsp"
#include "shuffle_notepad.hsp"
#include "modules/grid_system/area_grid_system.hsp"
#include "modules/grid_system/g_radiobox.hsp"
#include "modules/grid_system/g_button.hsp"
#include "modules/grid_system/g_chkbox.hsp"
#include "modules/grid_system/g_listbox.hsp"
#include "modules/grid_system/g_slider.hsp"
#include "modules/grid_system/g_stepper.hsp"
#include "modules/grid_system/g_label.hsp"
#include "modules/grid_system/g_treebox.hsp"
#include "modules/grid_system/g_line_graph.hsp"
#include "modules/group.hsp"
#include "go_game_manager.hsp"
#include "hspext.as"
#include "user32.as"

#define window_file_name "window.txt"
#const INACTIVITY_TIMEOUT 1800
#const MODE_KIFU_VIEW 0
#const MODE_BOARD_EDIT 1

screen , ginfo(20), ginfo(21)
GetWindowLong hwnd, -16
SetWindowLong hwnd, -16, stat | $10000 | $40000

#ifdef _debug
	dirname = "C:\\Users\\owner\\Desktop\\katago-kifu-search"
	#define 計測する
#else
	dirname = dir_exe
#endif
kifu_dirname = dirname + "/棋譜"

chdir dirname + "/save"
dirlist filelist, "*.sgf"
notesel filelist
	repeat notemax
		noteget tmp, cnt
		delete tmp
	loop
noteunsel

chdir dirname
exist window_file_name

if strsize == -1{
	x = 1140
	y = 855
	sx = x
	sy = y
	wx = (ginfo(20) - x) / 2
	wy = (ginfo(21) - y) / 2
	//探索の最大深さ
	max_depth = 70
	
	//色反転を含めるか
	enableColorFlip = 1
	
	//星を表示するか
	view_hosi@goban = 0

	//最終手表示
	last_move_mark = 1
	
	//棋譜を何手目から表示するか
	kifu_start_num = 10

	//棋譜再生速度(ms)
	auto_interval = 1000
	alpha_interval = 10000
	m_interval = 10000
	
}else{
	notesel s
	chdir dirname
	noteload window_file_name
	noteget_start
	noteget_ x
	noteget_ y
	noteget_ wx
	noteget_ wy
	noteget_ sx
	noteget_ sy
	noteget_ max_depth
	noteget_ enableColorFlip
	noteget_ view_hosi@goban
	noteget_ last_move_mark
	noteget_ kifu_start_num
	noteget_ auto_interval
	noteget_ alpha_interval
	noteget_ m_interval
	noteget_ 最小化
	noteget_ 最大化

	if wx < 0{
		wx = (ginfo(20) - x) / 2
		wy = (ginfo(21) - y) / 2
	}

	noteunsel
}

#module
#deffunc noteget_start
	tmp = 0
	return
#deffunc noteget_ var a
	noteget a, tmp
	a = int(a)
	tmp++
	return
#global

width sx, sy, wx, wy
save_window_size = ginfo(12), ginfo(13)
if 最大化: sendmsg hwnd, $112, $F030
if 最小化: sendmsg hwnd, $112, $F020
	
onexit *save_info
if 0{
	*save_info
	chdir dirname
	sdim s,1024
	notesel s
	noteadd str(ginfo(10)) //ウィンドウサイズ
	noteadd str(ginfo(11))
	noteadd str(ginfo(4)) //ウィンドウ位置
	noteadd str(ginfo(5))
	noteadd str(save_window_size  )　//描画領域サイズ
	noteadd str(save_window_size(1))
	noteadd str(max_depth)
	noteadd str(enableColorFlip)
	noteadd str(view_hosi@goban)
	noteadd str(last_move_mark)
	noteadd str(kifu_start_num)
	noteadd str(auto_interval)
	noteadd str(alpha_interval)
	noteadd str(m_interval)
	IsIconic hwnd: 最小化 = stat
	IsZoomed hwnd: 最大化 = stat
	noteadd str(最小化)
	noteadd str(最大化)
	notesave window_file_name
	save_load_manager_save
	end
}
randomize

header_size = 40
pos , header_size


//使用キーの宣言
delkey_all
addkey 1 //左クリック
addkey 2 //右クリック
addkey 17 //ctrl
addkey 'C'
addkey 'V'
addkey 'A'


v = "" //検索対象座標セット　xxyy,xxyy... 形式
search_result_sgflist = ""
jidou = 0
着手番号表示 = 0
ui_mode = MODE_KIFU_VIEW
set_treeview 37, 37
set_treeview_position 10,20
set_tree_direction 6

kifu_update = 1

topnode = -1

#ifdef init_mem
	init_mem max_depth
#endif

//エリア作成
root = make_area_child(1,1)
	
	body_area = make_area_child(3,1, root)
		sidebar_area = make_area_child(1,2,body_area)
		sidebar_buttons = make_area_child(2,3,sidebar_area)
		sidebar_kifu_area = make_area_child(1,2)
			sidebar_kifulist_area = make_area_child(2,,sidebar_kifu_area)
			
			sidebar_kifulist_buttons = make_area_child(2,2)

				sidebar_kifulist1 = make_area_child(,3,sidebar_kifulist_area)
				sidebar_kifulist2 = make_area_child(,3,sidebar_kifulist_area)

					sidebar_kifulist_label1 = make_area_child(,,sidebar_kifulist1)
					sidebar_kifulist = make_area_child()
					kifustart = make_area_child()

					sidebar_kifulist_label2 = make_area_child(,,sidebar_kifulist2)
					sidebar_kifulist_match = make_area_child()
					kifustart_result = make_area_child()
					
				sidebar_kifulist_match_back = make_area_child(,,sidebar_kifulist_buttons)
				sidebar_kifulist_match_next = make_area_child()
				chkbox_autoplay = make_area_child()
				slider = make_area_child()

			chkbox_last_move = make_area_child(,,sidebar_buttons)
			chkbox_view_hosi = make_area_child()
			button_getsgf = make_area_child()
			button_getsgf_nowpoint = make_area_child()
			button_download_file = make_area_child()
			button_save_file = make_area_child()
			
		goban_area = make_area_child(1,2,body_area)
				goban_body_area = make_area_child(,,goban_area)
				goban_footer = make_area_child(7,,goban_area)
					back_top = make_area_child(,,goban_footer)
					back_one = make_area_child()
					back_ten = make_area_child()
					go_ten = make_area_child()
					go_one = make_area_child()
					go_end = make_area_child()
					button_clearboard = make_area_child()
					
		goban_tool_area = make_area_child(1,3,body_area)
			goban_graph_area = make_area_child(1,2,goban_tool_area)
				treebox_area = make_area_child(,,goban_graph_area)
				line_graph_area = make_area_child()
			goban_search_area = make_area_child(2,3,goban_tool_area)
				button_search = make_area_child(,,goban_search_area)
				button_exclude_search = make_area_child()
				button_narrow_down_search = make_area_child()
				chkbox_colorflip = make_area_child()
				chkbox_use_bitboard = make_area_child()
				chkbox_inverse = make_area_child()
			goban_edit_area = make_area_child(2,1, goban_tool_area)
				goban_edit_stone = make_area_child(1,2,goban_edit_area)
					chkbox_tyakusyu = make_area_child(,,goban_edit_stone)
					radiobox_goban_edit = make_area_child()
				goban_edit_group = make_area_child(1,2,goban_edit_area)
					button_group_clear = make_area_child(,,goban_edit_group)
					radiobox_grouping = make_area_child()
					

exist "area.var"

if strsize >= 0{
	save_load_manager_load
}

chdir kifu_dirname
sdim titles
//各エリアの設定
set_grid_area_back_color sidebar_kifulist_area, $333333
set_grid_area_back_color goban_search_area,$333333
set_grid_area_back_color goban_edit_stone,$333333
set_grid_area_back_color goban_edit_group,$333333
set_grid_area_back_color goban_footer,$333333
set_grid_area_back_color treebox_area ,$333333
set_padding sidebar_kifulist1, 0,0,0,0
set_padding sidebar_kifulist2, 0,0,0,0
set_margin sidebar_kifulist1, 0,5
set_margin sidebar_kifulist2, 0,5


//実体作成
newmod g_buttons, grid_button, "最新棋譜取得", *loadfile, button_download_file
newmod g_buttons, grid_button, "保存",*save_file,button_save_file
newmod g_buttons, grid_button, "SGF取得"   , *getsgf  , button_getsgf
newmod g_buttons, grid_button, "SGF取得\n(現局面)", *getsgf_nowpoint, button_getsgf_nowpoint
newmod g_buttons, grid_button, "クリア"  , *clearboard, button_clearboard
newmod g_buttons, grid_button, "検索"          , *create_pattern, button_search
newmod g_buttons, grid_button, "除外検索"      , *create_pattern2, button_exclude_search
newmod g_buttons, grid_button, "絞込検索"  , *create_pattern3, button_narrow_down_search
newmod g_buttons, grid_button, "次の棋譜"  , *next_kifu, sidebar_kifulist_match_next
newmod g_buttons, grid_button, "前の棋譜"  , *back_kifu, sidebar_kifulist_match_back
newmod g_buttons, grid_button, "|<",*to_sgftop, back_top
newmod g_buttons, grid_button, "<<", *back10, back_ten
newmod g_buttons, grid_button, "<", *move_back, back_one
newmod g_buttons, grid_button, ">", *move_next, go_one
newmod g_buttons, grid_button, ">>", *go10, go_ten
newmod g_buttons, grid_button, ">|",*to_sgfend,go_end
newmod g_buttons, grid_button, "石グループ解除",*group_clear, button_group_clear

newmod g_chkbox, grid_checkbox, ui_mode           , "局面編集" , chkbox_tyakusyu
newmod g_chkbox, grid_checkbox, enableColorFlip,"検索に\n色反転も含む", chkbox_colorflip
newmod g_chkbox, grid_checkbox, jidou          ,"自動再生",chkbox_autoplay
newmod g_chkbox, grid_checkbox, view_hosi@goban,"星表示", chkbox_view_hosi
newmod g_chkbox, grid_checkbox, last_move_mark,"最終手表示",chkbox_last_move
newmod g_chkbox, grid_checkbox, use_bitboard, "厳密検索", chkbox_use_bitboard
newmod g_chkbox, grid_checkbox, inverse, "反転検索",chkbox_inverse

newmod g_label, grid_label,"棋譜リスト",sidebar_kifulist_label1, $F5F5DC,$333333
newmod g_label, grid_label,"検索結果(0)件",sidebar_kifulist_label2,$F5F5DC,$333333

newmod g_stepper, grid_stepper, kifu_start_num,kifustart: set_stepper_limit g_stepper(stat), 0
newmod g_stepper, grid_stepper, kifu_start_num_result, kifustart_result: set_stepper_limit g_stepper(stat), -50

newmod g_slider, grid_slider, auto_interval , 1, slider, 100, 10000, 14 , 16
newmod g_slider, grid_slider, alpha_interval, 1, slider, 0  , 10000, 14 , 16, , 25
newmod g_slider, grid_slider, m_interval    , 1, slider, 0  , 10000, 14 , 16, , 50

newmod g_radiobox, grid_radiobox, g_edit, radiobox_goban_edit
add_item_radiobox g_radiobox, "●"
add_item_radiobox g_radiobox, "〇"
add_item_radiobox g_radiobox, "●〇"
add_item_radiobox g_radiobox, "×"



newmod g_radiobox, grid_radiobox, g_group, radiobox_grouping : tmp = stat
add_item_radiobox g_radiobox.tmp, "A"
add_item_radiobox g_radiobox.tmp, "B"
add_item_radiobox g_radiobox.tmp, "C"
add_item_radiobox g_radiobox.tmp, "D"



newmod kifulist, grid_listbox, sidebar_kifulist
newmod kifulist, grid_listbox, sidebar_kifulist_match: matchlist = stat

newmod board, goban, 19,19,26,26, goban_body_area
newmod board, goban, 19,19,26,26, goban_body_area


newmod t, treebox,treebox_area
newmod l_graph, line_graph, line_graph_area
set_margin_line_graph l_graph, 15, 10, 15, 10

//矩形範囲指定用
rn = 0
dim rect, 4
dim rect_stack, 4

//検索結果受け取り用
sdim search_result,, 2, 10000
sdim search_result_sgflist

foreach kifulist
	set_listbox_item_ysize kifulist.cnt, 29
	set_listbox_scollbar_width kifulist.cnt, 15
loop

foreach g_label
	set_g_label_r g_label.cnt , 5, 4|8
loop


gosub*load_kifu_folder
	
gosub*loadsgf

c_list = "0.50"
dim pre_xy, 2
dim user_board, 19,19
pretime = keyfpstime

pre_window_size = -9999,-9999
pre_ui_mode = -1

setfps 60
*main
	;logmes ""+getrealfpsf()
	計測 "manage_keyfps"
	manage_keyfps
	計測 "前フレームからの状態変化取得"
	window_size = ginfo(12), ginfo(13)
	if pre_window_size != window_size || pre_window_size.1 != window_size.1{
		pre_window_size = window_size, window_size.1
		gosub *save_window_size_main
	}
	if pre_ui_mode != ui_mode {
		pre_ui_mode = ui_mode

		if ui_mode = MODE_KIFU_VIEW{
			group_init
		}else{
			get_board_data board  , user_board
			set_board_data board.1, user_board
		}
	}
	計測 "kifuload"
	if kifu_update{//棋譜がアップデートされたときのみ走る
		new_ = get_in_area()
		//マウスがエリア間を移動したか、プログラム上で左クリックされているか
		if (pre_ != new_) || ((_getkey(1) >= 1) && (ginfo_act != -1)){
			pat = keyfpstime
			pre_ = new_
		}
		//ユーザーがアクティブでない時
		if keyfpstime - pat >= INACTIVITY_TIMEOUT{
			
			//毎フレーム１棋譜読み込む
			
			notesel filelist
				repeat
				
					noteget sgf_file_name, next_kifuload_index
					next_kifuload_index++
					if sgf_file_name == ""{
						//全て読み込んだ
						kifu_update = 0
						break
					}
					if get_value(sgf_file_name) == NOT_DEFINED_HASH{
						notesel sgf
							chdir kifu_dirname
							noteload sgf_file_name
							new_value sgf_file_name, str(load_sgf(sgf)) //ファイル名とトップノードの番号を紐づける
						noteunsel
						break
					}
				loop
			noteunsel
			;/*/
		}
	}

	計測 "入力処理"
	
	nownode = get_current_node(t)
	getBoardCoordinates board, point

	in_board = 0
	if 0 <= point && point < 19{
		if 0 <= point.1 && point.1 < 19{
			in_board = 1
		}
	}
	if debugmode == 0{
		//マウス操作
		if _getkey(2) == 1{ //右クリック
			if treebox_onmouseid(t) >= 0{
				set_current_node t, treebox_onmouseid(t)
				Treebox_toggle_openflag t, treebox_onmouseid(t)
			}
			if ui_mode == MODE_BOARD_EDIT && dragging_flag == 0{//矩形描画モード
				if in_board{
					if treebox_onmouseid(t) < 0{
						if in_board{
							user_board(point,point.1) = (user_board(point,point.1)  + 1) \ 6
							set_board_data board.1, user_board
							if user_board(point,point.1) == 0{
								repeat 4
									del_group_stone point, point.1,cnt
								loop
							}
						}
					}
				}
			}
		}

		//右長押しで、指定された範囲を除外
		if gethold_ms(2,300,400){
			if rn{
				rn--
				modify_coordinates_by_rect v, rect_stack(0,rn), rect_stack(1,rn), rect_stack(2,rn), rect_stack(3,rn), 1
				dragging_flag = 0
			}
		}
		
		if _getkey(1) == 1{ //左クリック
	
			inlistbox = 0
	
			foreach kifulist
				sel = listbox_on_id(kifulist.cnt,mousex, mousey)
				inlistbox += select_kifulist_item(sel, cnt)
	
				if sel == -3{
					set_current_scroll_index_by_bar_position kifulist.cnt, mousey
				}
			loop
			
			if inlistbox == 0{
				dragging_flag = 1
				if in_board{
					if (ui_mode == MODE_KIFU_VIEW & g_group = -1) || (ui_mode != MODE_KIFU_VIEW && g_edit == -1){
						rect = limit(point,0,18),limit(point.1,0,18)
						rect(2) = rect, rect.1
					}
				}else{
					rect = -1
				}
				if treebox_onmouseid(t) >=0{
					set_current_node t, stat		
					sync_treebox_board t, board 
					手数 = treeview_onmouse_id_deep()
				}

	

			}
		}
	
		if dragging_flag{
			
			rect(2) = limit(point(0),0,18)
			rect(3) = limit(point(1),0,18)

			
			if in_board {
				switch g_edit

				swbreak
				case 0
					user_board(point,point.1) = 1
					set_board_data board.1, user_board
				swbreak
				case 1
					user_board(point,point.1) = 2
					set_board_data board.1, user_board
				swbreak
				case 2
					if prex = point.0 & prey = point.1{
					}else{
						prex = point.0
						prey = point.1
						swapturn^1
						user_board(point,point.1) = swapturn + 1
						set_board_data board.1, user_board
					}
				swbreak
				case 3
					user_board(point,point.1) = 0
					set_board_data board.1, user_board
					repeat 4
						del_group_stone point, point.1,cnt
					loop
				swbreak

				swend

				if g_group >= 0{
					
					if ui_mode == MODE_KIFU_VIEW{
						get_board_data board  , tmp
						OK = tmp(point,point.1)
					}else{
						OK = user_board(point,point.1)
					}
					if OK{
						add_group_stone point, point.1, g_group
					}
				}
			}
			if _getkey(1) == -1{//マウスが離されたとき
				
				dragging_flag = 0
				if rect(0) >= 0{
					modify_coordinates_by_rect v, rect(0), rect(1), rect(2), rect(3)
					rect_stack(0,rn) = rect(0), rect(1), rect(2), rect(3)
					rn++
					dim rect,4
				}
			}

			if _getkey(2) == 1{
				if rect(0) >= 0{
					modify_coordinates_by_rect v, rect(0), rect(1), rect(2), rect(3), 1
				}
				dragging_flag = 0
			}
		}
	
		//マウスホイール
		mw = mousew
	
		in_kifulist == 0
		foreach kifulist
			if listbox_on_id(kifulist.cnt,mousex, mousey) >= 0{
				in_kifulist++
				if mw < 0: move_scroll_index kifulist.cnt,1
				if mw > 0: move_scroll_index kifulist.cnt,-1
			}
		loop
		if (in_kifulist == 0) && (ui_mode == MODE_KIFU_VIEW){
			if mw < 0 : gosub*move_next
			if mw > 0 : gosub*move_back
		}
	
		//キー操作
		if _getkey(17) >= 1{ //CTRL
			if _getkey('C') == 1 { //CTRL + C
				gosub*getsgf
			}
			if _getkey('V') == 1 { //CTRL + V
				gosub*loadsgf
			}
			if _getkey('A') == 1{ //CTRL + A
				repeat get_item_num(kifulist.current_kifulist)
					if get_selected_status_(kifulist.current_kifulist, cnt) == 0{
						check_item kifulist.current_kifulist, cnt
					}
				loop
			}
		}
	
		//自動再生
		計測 "自動再生"

		//-------------------基本wait +  エンジンの形勢判断によるwait + 直前の手とのマンハッタン距離によるwait
		if jidou & (ui_mode == MODE_KIFU_VIEW){
			if pre_jidou == 0{
				前の形勢 = double(c_list)
				pre_jidou = 1
			}
			if pretime + auto_interval + (形勢の変化率 * alpha_interval) + (m_interval * マンハッタン距離 / 36)  <= keyfpstime{
				
				gosub *move_next
				今の形勢 = double(c_list)
				形勢の変化率 = absf(今の形勢 - 前の形勢)
				前の形勢 = 今の形勢
			}
		}else{
			pre_jidou = 0
			if ginfo_act == -1{
				skipdraw = 1
				skipcount++
			}
		}
	}

	計測 "描画"

	//背景ベースカラー描画
	color 128,128,128
	boxf 0, 0, ginfo_winx, ginfo_winy

	//碁盤エリアの背景に形勢を反映
	a = double(c_list) * 255: color a,a,a
	get_rect goban_area, goban_back_rect
	aboxf goban_back_rect

	if ui_mode == MODE_KIFU_VIEW{
		碁盤表示 board  , 着手番号表示,last_move_mark, 20
	}else{
		碁盤表示 board.1,             ,last_move_mark, 20
	}

	//エリア矩形表示
	draw_all_rect root, debugmode
	
	//グループ表示
	get_goban_mas_size board,goban_mas_size
	get_goban_position board,goban_position
	group_draw goban_position, goban_position.1, goban_mas_size, goban_mas_size.1, board.ui_mode
	//検索範囲表示
	draw_set_list_on_board board, v
	
	//各アイテム表示
	foreach kifulist
		my_listbox_draw kifulist.cnt
	loop
	foreach g_label
		draw_g_label g_label.cnt
	loop
	font "メイリオ", 29
	foreach g_chkbox
		draw_g_chkbox g_chkbox.cnt
	loop
	foreach g_buttons
		draw_g_button g_buttons.cnt
	loop
	foreach g_slider
		slider_manager g_slider.cnt
	loop
	foreach g_stepper
		draw_g_stepper g_stepper.cnt
	loop
	foreach g_radiobox
		draw_g_radiobox g_radiobox.cnt
	loop
	
	新しい手数 = 手数
	line_graph_manager l_graph, 新しい手数
	if 手数 > 新しい手数{
		repeat 手数 - 新しい手数
			gosub *move_back
		loop
	}else: if 手数 < 新しい手数{
		repeat 新しい手数 - 手数
			gosub *move_next
		loop
	}

	if dragging_flag && rect >= 0:碁盤に矩形データを表示 board, rect

	if getdbl_ms(17,250): debugmode ^ 1

	treebox_draw t, length(f)

	計測描画

	redraw
	redraw 2
	
	goto*main

#deffunc 計測 str time_str
	#ifdef 計測する
		if time_num == 0 : pre_time = keyfpstime
		if time_num >= 1 : times(time_num - 1) = keyfpstime - pre_time : pre_time = keyfpstime
		titles(time_num) = time_str
		time_num++
	#endif
	return

#deffunc 計測描画
	#ifdef 計測する
		計測 "" ; 最後の区間の記録
	
		s = ""
		repeat time_num - 1
			s += titles(cnt) + " -> " + strf("%03d", times(cnt)) + "ms :: "
		loop
		time_num = 0
	
		pos 0,0
		color 0,0,0
		font "",20
		mes s
	#endif
	return

*save_window_size_main
		IsZoomed hwnd :最大化 = stat
		IsIconic hwnd :最小化 = stat
		if (最大化 | 最小化) == 0{
			save_window_size = window_size, window_size.1
		}
	return
	
#defcfunc select_kifulist_item int itemid, int kifulist_id, int flag_next_or_back
	
	if itemid >= 0 && itemid < get_item_num(kifulist.kifulist_id){
		current_kifulist = kifulist_id
		ui_mode = MODE_KIFU_VIEW
		if get_root_tree_node(t) >= 0{
			del_treenode(get_root_tree_node(t))
		}
		set_treeview_scroll
		//範囲指定を初期化
		sdim v
		rn = 0

		//アイテムにチェックを付ける
		check_item kifulist.kifulist_id, itemid, _getkey(17) < 0

		if is_item_visible(kifulist.kifulist_id,itemid) == 0{
			if flag_next_or_back = 0{
				scroll_item_to_top kifulist.kifulist_id, itemid
			}else{
				scroll_item_to_bottom kifulist.kifulist_id, itemid
			}
		}
		
		items = get_name_all_items(kifulist.kifulist_id)
		notesel items
		noteget s, itemid
		noteunsel
		topnode = change_sgf(s, t, board)
		logmes "kifuclick -> "+ topnode

		clear_graph l_graph

		get_nodelist int(get_property_data(topnode,"MN")), f
		set_end_xy l_graph, length(f), 100

		for i,length(f)-1,-1,-1
			tmp = get_property_data(f(i),"C")
			split tmp," ",r
			add_graph_data l_graph, (1. - r) * 100
		next
		
		手数 = 0
		
		if get_listbox_item_data(kifulist.kifulist_id,itemid){ //アイテムデータが設定されていたら、それは検索結果。
			nstat = stat //検索結果のSGFノード
			
			//それが格納されているツリーIDを探してカレントノードに設定、手数も合わせる
			set_current_node t, search_tree_by_item_id(get_root_tree_node(t), nstat, 手数)

			
			nownode = get_current_node(t)
			repeat abs(kifu_start_num_result)
				if kifu_start_num_result > 0{
					gosub*move_next
				}else{
					gosub*move_back
				}
			loop

		}else{
			nownode = get_root_tree_node(t)
			repeat kifu_start_num
				gosub*move_next
			loop
		}

		gosub*syncboard
		
		return 1
	}
	return 0
*next_kifu
	logmes "NEXT"
	a = get_selected_items(kifulist(current_kifulist), sel)
	a = select_kifulist_item(sel + 1, current_kifulist,1)
	return
*back_kifu
	logmes "BACK"
	a = get_selected_items(kifulist(current_kifulist), sel)
	a = select_kifulist_item(sel - 1, current_kifulist)
	return
*go10
	repeat 10
		gosub*move_next
	loop
	return
	
*back10
	repeat 10
		gosub *move_back
	loop
	return
*to_sgftop
	set_current_node t
	gosub*syncboard
	return
	
*to_sgfend

	if topnode < 0: return
	repeat 1000
		gosub *move_next
		if stat == 0: break
		if cnt == 999: logmes "おそらくエラー"
	loop
	gosub*syncboard
	return
	
*clearboard
	if ui_mode == MODE_KIFU_VIEW{
		clear_board board
	}else{
		dim user_board,19,19
		set_board_data board.1,user_board
		group_init
	}
	treebox_clear t
	topnode = stat
	title ""
	current_kifulist = -1
	return
	
*getsgf_nowpoint
	if nownode < 0: return
	logmes get_tree_item_id(nownode)
	clipset get_node_sgf_nowpoint(get_tree_item_id(nownode))
	title "現局面をクリップボードにコピーしました"
	return
*getsgf

	dim nodelist
	
	repeat get_selected_items(kifulist.current_kifulist, s)
		nodelist(cnt) = int(get_value(get_name_item(kifulist.current_kifulist, s(cnt))))
	loop
	new_node = merge_node(nodelist)
	clipset get_node_sgf(new_node)
	del_sgf_node new_node, 1 //削除して完了
	title "棋譜をクリップボードにコピーしました"
	return
*loadsgf
	dim t
	newmod t, treebox, treebox_area
	sdim s, $FFFF
	clipget s, varsize(s)
	
	if load_sgf(s) < 0: return
	
	topnode = load_sgf_set_board(t, stat, board)
	
	blackplayer = get_property_data(topnode, "PB")
	whiteplayer = get_property_data(topnode, "PW")

	title" 黒 " + blackplayer + "   白 " + whiteplayer
	return
*group_clear
	if g_group = -1{
		repeat 4
			clear_group cnt
		loop
	}else{
		clear_group g_group
	}
	
	return
*save_file
	tmpdir = dir_cur
	savedir = "save"

	dim nodelist
	repeat get_selected_items(kifulist.current_kifulist, node_ids)
		nodelist(cnt) = int(get_value(get_name_item(kifulist.current_kifulist, node_ids(cnt))))
	loop
	new_node = merge_node(nodelist)
	s = get_node_sgf(new_node)
	notesel s
		logmes dirname + "/" + savedir + "/" + get_name_item(kifulist.current_kifulist, node_ids(0))
		notesave dirname + "/" + savedir + "/" + get_name_item(kifulist.current_kifulist, node_ids(0))
	noteunsel

	del_sgf_node new_node, 1 //削除して完了
	
	chdir tmpdir

	title "棋譜をSGF形式で保存しました"
	return
*create_pattern
	time = keyfpstime
	kl = kifulist_notepad
	gosub*create_pattern_main
	return
*create_pattern2
	time = keyfpstime
	kl = notepad_minus_notepad(kifulist_notepad, search_result_sgflist)
	logmes strmid(kifulist_notepad,0,50)
	gosub*create_pattern_main
	return
*create_pattern3
	time = keyfpstime
	kl = search_result_sgflist
	gosub*create_pattern_main
	return
*create_pattern_main
	logmes "検索開始"


	if ui_mode == MODE_KIFU_VIEW{
		get_board_data board, m
	}else{
		get_board_data board.1, m
	}

	if use_bitboard{
		
		result_num = search2(kl, m, search_result, max_depth, enableColorFlip, v, kifu_update, next_kifuload_index, inverse)
		
	}else{

		initialize_rects v, r, stones
		
		repeat load_stones_from_board(m, n)
			add_stone_pattern r, stones, n(0,cnt), n(1,cnt), n(2,cnt)
			logmes n(2,cnt)
		loop
	
		create_pattern_with_rects_and_stones r,stones,p
	
		logmes patterns_to_notepad(p)
		
		result_num = search(kl, p, search_result, max_depth, enableColorFlip, kifu_update, next_kifuload_index,inverse)
	}

	//早期リターンなら何もせず終わる。
	if result_num < 0: return
	
	kifu_update = 0

	search_result_sgflist = ""
	
	repeat result_num
		search_result_sgflist += search_result(0, cnt) + "\n"
	loop

	if search_result_sgflist != ""{

		sortnote search_result_sgflist, 1 //結果を最新棋譜順にソート
		current_kifulist = matchlist
	}

	del_item_all kifulist.matchlist
	add_item kifulist.matchlist, search_result_sgflist
	
	repeat result_num
		sortget n, cnt
		add_item_data kifulist.matchlist, cnt, search_result(1,n)
	loop

	repeat 4
		clear_group cnt
	loop
	logmes "検索終了 -> " +(keyfpstime - time) + "ms  " + result_num + "件の検索結果"

	
	set_g_label g_label.1, "検索結果(" + result_num + ")件"
	return
	
*loadfile
	chdir kifu_dirname
		棋譜取得 kifu_dirname, 100 // katagoホームページから最新棋譜をダウンロード
	chdir dirname

	gosub*load_kifu_folder
	
	return

*move_next
	if nownode < 0: return

	set_current_node t, get_tree_root_child(nownode)
	moved = stat
	if stat: 手数++: treeview移動 1
	pretime = keyfpstime
	gosub*syncboard
	return moved
	
*move_back
	if nownode < 0: return
	if get_tree_parent(nownode) != -1{
		set_current_node t, get_tree_parent(nownode)
		手数--
		pretime = keyfpstime
		treeview移動 -1
	}
	pretime = keyfpstime
	gosub*syncboard
	return
	
*syncboard
	nownode = get_current_node(t)
	if nownode < 0: return

	tmp = get_property_coordinate_cash(get_tree_item_id(nownode), x, y)

	マンハッタン距離 = abs(x - pre_xy)   +   abs(y - pre_xy.1)

	pre_xy = x, y

	if get_tree_item_id(nownode) == topnode{
		tmp = "0.50"
	}else{
		tmp = get_property_data(get_tree_item_id(nownode), "C")　//katago-rating-gameには、コメントに形勢判断が入ってるので取得
	}
	sync_treebox_board t, board

	split tmp," ", c_list
	
	return
	
*load_kifu_folder
	treebox_clear t
	chdir kifu_dirname
	del_item_all kifulist
	dirlist files,"*.sgf"
	sdim ss
	notesel ss 
		ss = shuffle_notepad(files)
		sdim filelist
		repeat notemax
			noteget tmp, cnt
			filelist += tmp + "\n"
		loop
		set_g_label g_label, "棋譜リスト("+notemax+")件"
	noteunsel
	add_item kifulist, filelist
	kifu_update = 1
	kifulist_notepad = filelist
	return
	
#deffunc treeview移動 int p1
	get_treeview_node_size ns 
	if (get_tree_direction() & 1){
		move_treeview_position , ns.1 * (((get_tree_direction() & 2) != 0) * 2 - 1) * p1
	}else{ 
		_move_treeview_position   ns  * (((get_tree_direction() & 2) != 0) * 2 - 1) * p1
	}
	return