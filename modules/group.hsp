#ifndef group_manager
#module group_manager

#const BOARD_X_MAX 100
#const BOARD_Y_MAX 100

#deffunc group_init
	group_str = "A","B","C","D" //４つまで指定できるようにする
	dim stone_map, BOARD_X_MAX, BOARD_Y_MAX, 4
	dim stone_map_count, BOARD_X_MAX, BOARD_Y_MAX
	sdim groups, 361, 4 //グループごとの位置を保存(xxyy, xxyy... )形式
	return
#deffunc group_draw int posx, int posy, int MAS_X, int MAS_Y, var board　 //碁盤が表示されている位置ｘ、ｙ、碁盤のマスの大きさx,y , 石情報（これによってグループ表示の文字色を変える）
	for i,,BOARD_X_MAX
		for j,,BOARD_Y_MAX
			repeat 4
				if stone_map(i,j, cnt){
					color_type = get_goban_position_color(board,i,j)
					text_color = select_group_color(color_type)
					s = group_str(cnt)
					sx = MAS_X
					sy = MAS_Y

					cx = posx + i * MAS_X
					cy = posy + j * MAS_Y
					
					if stone_map_count(i, j) >= 2 {
						sx /= 2
						sy /= 2
						cx += (cnt \ 2) * sx + sx / 2
						cy += (cnt / 2) * sy + sy / 2
					}else{
						cx += sx / 2
						cy += sy / 2
					}

					make_instant_draw_object "STR", cx, cy, sx, sy, strf("%s,%d", group_str(cnt), text_color)
				}
			loop
		next
	next
	return
#defcfunc select_group_color int c
	switch c
	case 0
	case 2
	case 3
	case 5
	case 6 : return "$000000" ; 黒文字
	case 1
	case 4 : return "$FFFFFF" ; 白文字
	default : dialog "想定外の石の種類です",,__FILE__: end
	swend
	
#deffunc add_group_stone int x, int y, int group_num
	if group_num_validation(group_num){
		return
	}
    if stone_map(x, y, group_num) == 0{
 		stone_map(x, y, group_num) = 1
    	stone_map_count(x, y)++
    	groups(group_num) += strf("%02d%02d,",x,y)
	}
    
	return
#deffunc del_group_stone int x, int y, int group_num
	if group_num_validation(group_num){
		return
	}
	if stone_map(x, y, group_num){
		stone_map(x, y, group_num) = 0
		stone_map_count(x, y)--
		index = instr(groups(group_num),0,strf("%02d%02d",x,y))
		groups(group_num) = strmid(groups(group_num),0,index) + strmid(groups(group_num), index + 5, 65535)
	}
	return

#deffunc get_stones_by_group array a, int p1
	split groups(p1),",",tmp
	repeat stat - 1
		a(0,cnt) = 0 + strmid(tmp(cnt),0,2)
		a(1,cnt) = 0 + strmid(tmp(cnt),2,2)
	loop
	return
	
#defcfunc get_group_by_position int x, int y
	return stone_map(x, y, 3) << 3 | stone_map(x, y, 2) << 2 | stone_map(x, y, 1) << 1| stone_map(x, y, 0)

//回転に対応。検索前に作成して利用
#deffunc make_rotated_group array rotation_table, int board_size_x, int board_size_y
	is_square = (board_size_x == board_size_y)
	num_rotations = 4 << is_square //基本４パターン、正方形は８種類
	dim rotation_table, board_size_x, board_size_y, num_rotations 
	repeat length(groups)
		group_flag = 1 << cnt
		split groups(cnt),",", coordinate_list
		
		repeat stat
			coord_str = coordinate_list(cnt)
			if strlen(coord_str) < 4: continue
			original_x = int(strmid(coord_str,0,2))
			original_y = int(strmid(coord_str,2,2))
			
			repeat num_rotations
				rotate_coordinate xy, original_x, original_y, cnt, board_size_x, board_size_y
				rotation_table(xy, xy(1) , cnt) |= group_flag
			loop
		loop
	loop
	return

#deffunc clear_group int group_num
    if group_num_validation(group_num) {
        return
    }
    
    // グループに登録された座標を取得
    split groups(group_num), ",", tmp
    repeat stat
        xy = tmp(cnt)
        if strlen(xy) == 4 {
            x = int(strmid(xy, 0, 2))
            y = int(strmid(xy, 2, 2))
            if stone_map(x, y, group_num) {
                stone_map(x, y, group_num) = 0
                stone_map_count(x, y)--
            }
        }
    loop

    // グループデータをリセット
    groups(group_num) = ""
    
    return
#defcfunc group_num_validation int group_num
	invalid = group_num < 0 || group_num > 3
	if invalid{
		logmes "不正なグループ値です" + group_num
	}
	return invalid
#global
group_init
#endif