#ifndef sgf_node
#include "bitboard.hsp"
#module sgf_node

#define KEY_VALUE_SEP "|||"

#define MAX_NODE 2500000

#deffunc init_sgf_module
	dim parent, MAX_NODE //親ノード
	
	dim sgf_node_turn, MAX_NODE //ノードの手番
	dim sgf_node_x, MAX_NODE //ノードの座標ｘ
	dim sgf_node_y, MAX_NODE //ノードの座標ｙ
	sdim sgf_properties_data,, MAX_NODE //その他のプロパティ
	dim sgf_properties_num, MAX_NODE //その他のプロパティの数
	
	repeat bitboard_used_count
		del_bitboard bitboard(bitboard_used(cnt))
		#ifdef _debug
			await
			logmes ""+bitboard_used_count + ": "+ cnt
		#endif
	loop
	bitboard_used_count = 0
	dim bitboard_used, MAX_NODE //ビットボード使用ノードリスト(SGFノード番号)
	dim node_to_bitboard_used_list, MAX_NODE //ノードがビットボード使用ノードリストのどこに配置されているか。１スタートの値を保存(0= ビットボード未使用, 1 =bitboard_used(0) , 2 =bitboard_used(1) )
	dim bitboard, MAX_NODE //ビットボード(番号を保存、実体は別モジュール管理）
	
	sdim sgf_node_childs,, MAX_NODE //子ノードリスト
	dim child_num, MAX_NODE //子ノードの数

	//------------------------------------
	sdim open_id_list, MAX_NODE * 4 //配列の未使用番号リスト
	lpoke open_id_list, 0, 0
	open_id_list_num = 1 //配列の未使用番号の数
	node_count = 1 //配列のサイズ（未使用要素も含む）

	SEP = " "
	poke SEP, 0, $1F
	
	return

#defcfunc make_new_SGFnode int p1
	if open_id_list_num{
		open_id_list_num--
		new_id = lpeek(open_id_list, open_id_list_num * 4)
	}else{
		new_id = node_count
		node_count++
	}

	init_node new_id, p1
	
	return new_id

#deffunc add_sgf_property int node_id, str s1, str s2

	sgf_properties_data(node_id) += s1 + KEY_VALUE_SEP + s2 + SEP
	sgf_properties_num(node_id)++
	
	return
#deffunc del_sgf_property int node_id, str s1

	dup data, sgf_properties_data(node_id)
	
	index = instr(data, 0, SEP + s1 + KEY_VALUE_SEP)
	if index == -1: return
	
	next_index = index + 1 + instr(data, index + 1, SEP)

	left = strmid(data, 0, index)
	right = strmid(data, next_index, 999999)

	data = left + right

	sgf_properties_num(node_id)--

	return

#deffunc add_SGFchild int node_id, int p1
    if search_SGFchild(node_id, p1) == -1 {
        index = child_num(node_id) * 4  // 4バイト単位で格納
        memexpand sgf_node_childs(node_id), index + 4
        lpoke sgf_node_childs(node_id), index, p1
        child_num(node_id)++
    }
    return

#deffunc del_sgf_node int node_id, int flag, local child, local i, local num
	if flag{
		num = get_sgf_childs(node_id,child)
		for i,,num
			del_sgf_node lpeek(child, i * 4) , 1
		next
	}
	if bitboard(node_id) != -1 {
        del_bitboard(bitboard(node_id))
        bitboard_used_count--
       	bitboard_used(node_to_bitboard_used_list(node_id) - 1) = bitboard_used(bitboard_used_count) //末尾から移動
        node_to_bitboard_used_list(bitboard_used(bitboard_used_count)) = node_to_bitboard_used_list(node_id) //移動されたノードの情報更新
        node_to_bitboard_used_list(node_id) = 0
    }
	lpoke open_id_list, open_id_list_num * 4, node_id
	open_id_list_num++
	init_node node_id, -1
	return

#deffunc set_property_move int node_id, int turn, int x, int y
	sgf_node_turn(node_id) = turn
	sgf_node_x(node_id) = x
	sgf_node_y(node_id) = y
	return
	
#deffunc set_property_data int node_id, str s, str d
	if s == "B"{
		tmp = d
		set_property_move node_id, 1, peek(tmp, 0) - 'a', peek(tmp, 1) - 'a'
	}else: if s == "W"{
		tmp = d
		set_property_move node_id, 2, peek(tmp, 0) - 'a', peek(tmp, 1) - 'a'
	}else{
		//登録
		del_sgf_property node_id, s
		add_sgf_property node_id, s, d
	}
	return

#defcfunc get_property_data int node_id, str property_name
	dup data, sgf_properties_data(node_id)
	
	// プロパティの名前と値を探す
	index = instr(data, 0, SEP + property_name + KEY_VALUE_SEP)
	if index == -1: return ""  // 見つからなかった場合は空文字を返す
	
	// データの最後までのインデックスを取得
	next_index = index + 1 + instr(data, index + 1, SEP) //最後のSEPは必ず存在する事はモジュール全体で保証。
	
	value_start = index + 1 + strlen(property_name) + strlen(KEY_VALUE_SEP) //+1はstrlen(SEP)
	value_end = next_index

	// プロパティの値を抽出して返す
	result = strmid(data, value_start, value_end - value_start)
	
	;logmes "@@@@" + result
	return result

#defcfunc get_property_coordinate_cash int node_id, var x, var y
	x = sgf_node_x(node_id)
	y = sgf_node_y(node_id)
	return sgf_node_turn(node_id)

#defcfunc get_SGF_childs int node_id, array a
	dup a, sgf_node_childs(node_id)
	return child_num(node_id)

#defcfunc search_SGFchild int node_id, int target
	result = -1
    repeat child_num(node_id)
        if lpeek(sgf_node_childs(node_id), cnt * 4) == target{
	    	result = cnt
	    	break
	    }
    loop
    return result

#defcfunc search_SGFchild_byxy int node_id, int _x, int _y, int turn, local child

	result = -1
	repeat get_SGF_childs(node_id, child)
		if turn == get_property_coordinate_cash(lpeek(child, cnt * 4), x, y){
			if _x == x && _y == y{
				result = lpeek(child, cnt * 4): break
			}
		}
	loop
	
	return result


#defcfunc get_node_sgf int p1
	result = "("
	_get_node_sgf p1, result
	result += ")"
	return result

#deffunc _get_node_sgf int node_id, var s, local i, local n, local child

	s += ";"
	
	if sgf_node_turn(node_id) != -1{
		座標 = "  "
		poke 座標, 0, 'a' + sgf_node_x(node_id)
		poke 座標, 1, 'a' + sgf_node_y(node_id)
		
		if sgf_node_turn(node_id) == 1{
			s += "B[" + 座標 + "]"
		}else{
			s += "W[" + 座標 + "]"
		}
	}

	split sgf_properties_data(node_id), SEP, tmp
	repeat sgf_properties_num(node_id), 1
		split tmp(cnt), KEY_VALUE_SEP, kv
		if is_list_property(kv){
			s += kv
			repeat strlen(kv.1) / 2
				s += "[" + strmid(kv.1,cnt*2,2) + "]"
			loop
		}else{
			s += kv + "[" + kv.1 + "]"
		}
	loop
	
	n = get_SGF_childs(node_id, child)
	for i,, n
	
		if n >= 2{
			s+="("
		}
		
		_get_node_sgf lpeek(child, i * 4), s
		
		
		if n >= 2{
			s+=")"
		}
	next
	return

#deffunc get_nodelist int node_id, array a
	dim a
	_get_nodelist node_id, a
	return
#deffunc _get_nodelist int node_id, array a, int num
	a(num) = node_id
	if parent(node_id) != -1{
		_get_nodelist parent(node_id), a, num + 1
	}
	return

//特定のノードまでのSGFを取得
#defcfunc get_node_sgf_nowpoint int p1, local i
	get_nodelist p1, nodelist
	result = "("
	for i, length(nodelist)-1,-1,-1
		result += get_node_sgf_nowpoint_main(nodelist(i))
	next
	result += ")"
	return result
	
#defcfunc get_node_sgf_nowpoint_main int node_id, local i
	i = ";"
	if sgf_node_turn(node_id) != -1{
		座標 = "  "
		poke 座標, 0, 'a' + sgf_node_x(node_id)
		poke 座標, 1, 'a' + sgf_node_y(node_id)
		if sgf_node_turn(node_id) == 1{
			i += "B[" + 座標 + "]"
		}else{
			i += "W[" + 座標 + "]"
		}	
	}

	split sgf_properties_data(node_id), SEP, tmp
	repeat sgf_properties_num(node_id), 1
		split tmp(cnt), KEY_VALUE_SEP, kv
		if is_list_property(kv){
			i += kv
			repeat strlen(kv.1)/2
				i += "[" + strmid(kv.1,cnt*2,2) + "]"
			loop
		}else{
			i += kv + "[" + kv.1 + "]"
		}
	loop
	return i

#deffunc get_board_size_from_SGF int SGF_topnode, array xy

	size = get_property_data(SGF_topnode, "SZ")
	if size == ""{
		logmes "SGFにボードサイズが設定されていません"
		xy = 19,19
	}else{
		split size,":", x, y
		if stat = 1: y = x
		xy = int(x), int(y)
	}
	return
#defcfunc fetch_or_create_SGFnode int nownode, array coordinate, int 手番

	if nownode >= 0{
		if search_SGFchild_byxy(nownode, coordinate, coordinate.1 , 手番) >= 0{
			return stat //すでにノードが存在
		}
	}
	
	new = make_new_SGFnode(nownode)
	
	if nownode >= 0{
		add_SGFchild nownode, new
	}
	
	set_property_move new, 手番, coordinate, coordinate.1
		
	return new

//  NEW!!!
#defcfunc get_bitboard int node_id, local parent_id
    if bitboard(node_id) == -1 {
        // 親ノードの存在チェック
        parent_id = parent(node_id)
        
        // ルートノードの場合の初期化
        if parent_id == -1 {
            get_board_size_from_SGF node_id, size
            if (size(0) == 0) | (size(1) == 0) {
                // デフォルト19路盤
                bitboard(node_id) = make_new_bitboard(19, 19)
            } else {
                bitboard(node_id) = make_new_bitboard(size(0), size(1))
            }
        }else{
            // 親ノードからビットボードを継承
            if bitboard(parent_id) == -1 {
                // 親ノードも未初期化の場合は再帰的に初期化
                tmp = get_bitboard(parent_id)
            }
            bitboard(node_id) = copy_bitboard(bitboard(parent_id))
        }

        // SGFノードの着手情報を取得
        turn = sgf_node_turn(node_id)
        x = sgf_node_x(node_id)
        y = sgf_node_y(node_id)

        // パス（座標が-1）でない場合のみ着手処理
        if (x != -1) & (y != -1) {
	        //囲碁のルールに基づいて着手
            if play_move(bitboard(node_id), x, y, turn) < 0 {
               logmes "着手が無効です。SGFが壊れているかもしれません : " + __FILE__ 
            }
        }

		bitboard_used(bitboard_used_count) = node_id
		bitboard_used_count++
		node_to_bitboard_used_list(node_id) = bitboard_used_count
        
    }
    return bitboard(node_id)
    
#defcfunc is_list_property str key
    // リスト型プロパティの判定
    list_props = ",AB,AW,AE,TR,SQ,CR,LB,TB,TW,MA,SL,"
    return (instr(list_props, 0, "," + key + ",") >= 0)

#defcfunc is_text_property str key
    // テキスト型プロパティの判定
    text_props = ",C,N,GC,DM,GB,GW,HO,US,VW,"
    return (instr(text_props, 0, "," + key + ",") >= 0)
    
//マージ処理

#defcfunc is_ancestor int target, int ancestor
    current = target
    while current != -1
        if current == ancestor : return 1
        current = parent(current)
    wend
    return 0
    
#defcfunc compare_nodes int node1, int node2
	if parent(node1) == -1 && parent(node2) == -1: return 1
    // ノードのプロパティ比較
    return (sgf_node_x(node1) == sgf_node_x(node2)) && (sgf_node_y(node1) == sgf_node_y(node2)) && (sgf_node_turn(node1) == sgf_node_turn(node2))
    
#defcfunc get_node_properties int node_id
    // ノード情報の文字列化
    return "(" + sgf_node_x(node_id) + "," + sgf_node_y(node_id) + ") " + sgf_node_turn(node_id)

#deffunc connect_sgf_node int parent_node_id, int child_node_id, local i
	if child_node_id == -1: return
	if parent_node_id == -1{
		parent(child_node_id) = -1
		return
	}
	
	//双方向循環参照チェック
	if is_ancestor(parent_node_id, child_node_id) || is_ancestor(child_node_id, parent_node_id){
        logmes "Error: Circular reference detected! parent=" + parent_node_id +"child=" + child_node_id
        return
    }
    
    // プロパティベース重複チェック
    for i,, child_num(parent_node_id)
        current_child = lpeek(sgf_node_childs(parent_node_id), i*4)
        if compare_nodes(child_node_id, current_child) {
            logmes "Warning: Duplicate node. properties=" + get_node_properties(child_node_id)
            return
        }
    next

    ; mes parent_node_id,1
    ;mes "," + child_node_id

	//子ノード接続処理
	memexpand sgf_node_childs(parent_node_id), child_num(parent_node_id) * 4 + 4
	lpoke sgf_node_childs(parent_node_id), child_num(parent_node_id) * 4, child_node_id
	child_num(parent_node_id)++
	parent(child_node_id) = parent_node_id

	return
	
#deffunc copy_properties int node1, int node2
	if (node1 < 0) || (node2 < 0): return
	sgf_node_turn(node1) = sgf_node_turn(node2)
	sgf_node_x(node1) = sgf_node_x(node2)
	sgf_node_y(node1) = sgf_node_y(node2)
	sgf_properties_data(node1) = sgf_properties_data(node2) //その他のプロパティ
	sgf_properties_num(node1) = sgf_properties_num(node2) //その他のプロパティの数
	;logmes sgf_properties_num(node1)
	return

#defcfunc deep_copy_sgf_node int node_id, local i, local num, local c, local new_node, local sn

	stack(sn) = node_id ;;コピー元
	stackp(sn) = -1 ;;新たなノードが所属すべき番号
	sn++
	result = -1
	while sn
		sn--
		nn = stack(sn)
		p = stackp(sn)
    	new_node = make_new_SGFnode(-1) // 空ノードを作成
    	
    	if result = -1{
	    	result = new_node
	    }
  		copy_properties new_node, nn // 元のノードの情報をコピー
  		
 		connect_sgf_node p, new_node // ノード接続
 		
   		repeat get_sgf_childs(nn,c)
	    	stack(sn) = lpeek(c, cnt * 4)
	    	stackp(sn) = new_node
	    	sn++
      	loop
      	
    wend
    return result

#defcfunc has_child int node_id, int node_id2, local c
	result = -1
	repeat get_SGF_childs(node_id, c)
		mes "" + lpeek(c,cnt*4) + "   " + node_id2
		if compare_nodes(lpeek(c, cnt * 4), node_id2){
			result = lpeek(c, cnt*4)
			mes "" + result + ":" + node_id2 + "  " + node_id 
			break
		}
	loop
	return result

#defcfunc merge_node array node_list
	new = make_new_SGFnode(-1) //新たなトップノードを作成する
	repeat length(node_list)
		copied_node = deep_copy_sgf_node(node_list(cnt)) //ディープコピーしてそのノード番号を得る
		merge_node_main new, copied_node //マージ実行
		
		if length(node_list) >= 10{//１０棋譜以上であればフィードバックつける。
			title "マージ実行中 " + cnt * 100 / length(node_list) + "右クリック長押しで中断"
			if gethold_ms(2, 1000): break
			
			manage_keyfps
			await 10
		}
	loop
	return new

#deffunc merge_node_main int topnode1, int topnode2, local i, local c, local sn
	stack1(sn) = topnode1
	stack2(sn) = topnode2
	stack3(sn) = 0 //子ノード処理済みか
	sn++
	while sn
		sn--
		tn1 = stack1(sn)
		tn2 = stack2(sn)
		flag = stack3(sn)
		if flag = 0{
			stack3(sn) = 1
			sn++
			for i,,get_SGF_childs(tn2, c) //全ての子供をマージする
				tn2_child = lpeek(c, i * 4)
				if has_child(tn1, tn2_child) >= 0{ //共通ノード発見
					stack1(sn) = stat
					stack2(sn) = tn2_child
					stack3(sn) = 0
					sn++
				}else{
					connect_sgf_node tn1, tn2_child	
				}
			next
		}else{
			merge_properties tn1, tn2 //棋譜情報をマージ
			del_sgf_node tn2
		}
	wend
	
	return
#deffunc merge_properties int node_id, int node_id2

	copy_properties node_id, node_id2
	
	return

//////////マージ処理ここまで/////////////////////////////////////////////////////////
	
#deffunc local init_node int node_id, int p1, local c

	/*
	if parent(node_id) != -1{
		repeat get_sgf_childs(parent(node_id), c)
			if lpeek(c,cnt*4) == node_id{
				child_num(parent(node_id))--
				lpoke sgf_node_childs(parent(node_id)), cnt*4, lpeek(sgf_node_childs(parent(node_id)), child_num(parent(node_id))*4 )
			}
		loop
		;end
	}
	//*/

	parent(node_id) = p1
	
	sgf_node_turn(node_id) = -1
	sgf_node_x(node_id) = -1
	sgf_node_y(node_id) = -1
	bitboard(node_id) = -1  //---   NEW !!!
	
	sgf_properties_data(node_id) = SEP //SEPで始まりSEPで終わる形式で保存 (splitしたら、先頭と末尾は空になることに注意)
	sgf_properties_num(node_id) = 0
	
	sgf_node_childs(node_id) = ""
	child_num(node_id) = 0

	return

#global
init_sgf_module

#module
#defcfunc 対応する文字まで取得 var ss, int index, str s1, var result
	i = instr(ss, index, s1)
	result = strmid(ss, index, i)
	return index + i
#global

#module
#define global ctype load_sgf(%1, %2 = -1) _load_sgf(%1, %2)
#defcfunc _load_sgf str ss, int node
	
	sss = ss
	sgflen = strlen(sss)
	if sgflen < 3{
		//最小SGFは　(;)
		return -1
	}
	if (peek(sss) != '(') || (peek(sss, sgflen - 1) != ')'){
		return -1
	}
	start_point = instr(sss, 0 ,";")
	if start_point == -1{
		return -1
	}
	dim stack_count
	nc = 0
	MN = 0	//手数をカウントする独自プロパティ

	topnode = make_new_SGFnode(node)
	load_sgf_main strmid(sss, start_point + 1, sgflen - start_point - 2), topnode
	set_property_data topnode, "MN",str(lastnode)
	return topnode

#deffunc load_sgf_main str ss, int node
	sgfs = ss
	sgf_length = strlen(sgfs)
	nn = node
	point = 0
	//解析
	while point < sgf_length
		
		token = peek(sgfs, point)

		if (token == ';'){
			
			new_id = make_new_SGFnode(nn)
			add_SGFchild nn, new_id
			nn = new_id
			nc++
			if MN < nc: MN = nc:lastnode = nn
			
		}else:if token >= 'A' && token <= 'Z'{
			
			point = 対応する文字まで取得(sgfs, point, "[", P)
			point = 対応する文字まで取得(sgfs, point + 1, "]", D)
			set_property_data nn, P, D
			
		}else:if token == '['{
			
			point = 対応する文字まで取得(sgfs, point + 1, "]", D)
			set_property_data nn, P, get_property_data(nn, P) + D
			
		}else:if token = '('{
			
			stack(pn) = nn
			stack_count(pn) = nc
			pn++

			
		}else:if token == ')'{
			
			pn--
			nn = stack(pn)
			nc = stack_count(pn)
			
		}else{
			
			logmes "不正なトークンが含まれています"
			
		}
		point++
	wend
	return
#global
#endif