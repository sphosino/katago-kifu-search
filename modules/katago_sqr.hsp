#include "hspinet.as"
#addition "../../hsp_common/keyfps.hsp" //先インクルード必須モジュール
netinit
#include "beautifulsoup.hsp"

#module

#const DEFAULT_LOADTIME 30000
#const MIN_WAIT_TIME 1000

#deffunc 棋譜取得 str dirname, int max, int waittime, int loadtime
	stopflag = 0
	endflag = 0
	starttime = keyfpstime
	get_count = 0
	dim result
	now_dir = dir_cur
	
	wt = waittime
	if wt < MIN_WAIT_TIME{
		wt = MIN_WAIT_TIME
	}
	lt = loadtime
	if lt == 0{
		lt = DEFAULT_LOADTIME
	}
	
	url = "https://katagotraining.org"
	url2 = "games"
	
	chdir dirname
	dirlist files, "*.sgf"

	
	neturl@ url
	netrequest_get@ url2
	if stat : goto *bad	; 正しくリクエストができなかった
	gosub *通信待ち

	netgetv@ buf

	find_tags_bytag buf, "table", result
	find_tags_bytag result, "a", r
	
	repeat length(r) / 2
		find_attribute_byname r(1+cnt*2), "href", f


		next_link = ""


		*next_page
		logmes next_link
		neturl@ url + f
		netrequest_get@ next_link
		if stat : goto *bad
		gosub *通信待ち
		
		netgetv@ buf

	
		find_tags_bytag buf, "tr", data // テーブルの要素
		
		repeat limit(max,,length(data)-1)
			if endflag: break
		
			find_tags_bytag data(cnt + 1), "td", target_infos
	
			game_id = int(get_content_tag(target_infos(0)))
	
			title "最新棋譜チェック中"+ game_id + "::" +next_link+　"   " + limit((keyfpstime - starttime) * 100 / lt,,100)+"%完了"
			if keyfpstime - starttime >= lt: break
			if check_exist_sgf(files, game_id){
				await 4
				continue
			}
			await wt
	
			find_tags_bytag data(cnt + 1), "a", result
			find_attribute_byname result, "href", t
	
			netdlname@ dirname + "/" + game_id + ".sgf"
	
			neturl@ url + t
	
			logmes "取得開始 -> " + game_id + ".sgf"
			
			netrequest@ ""
			if stat : goto *bad
			gosub *通信待ち

			validate_sgf_file ""+ game_id + ".sgf"
			if stat{
				logmes "ファイル破損 -> " + game_id + ".sgf"
			}else{
				logmes "取得完了 -> " + game_id + ".sgf"
			}
			
			get_count++
			await
		loop
		if endflag:break
		if keyfpstime - starttime >= lt: break
		await wt

		if get_count < max{
			find_tags_bytag buf, "div",divs
			find_tags_bytag divs.4,"a",pp
			find_attribute_byname pp,"href", next_link
	
			if next_link == "?"{
				if length(pp) == 2{
					continue
				}
				find_attribute_byname pp.2,"href",next_link
			}
			if next_link == "https://github.com/lightvector/KataGo": continue
			goto *next_page
		}
	loop
	title "棋譜取得終了"
	chdir now_dir
	return

*通信待ち
	; 結果待ちのためのループ
	netexec@ res
	if res > 0 : return
	if res < 0 : goto *bad
	await 40
	goto *通信待ち


*bad
	; エラー処理
	dialog "ERROR -> " +res,,__FILE__

	end
#deffunc 棋譜取得停止
	endflag = 1
	return 
#defcfunc check_exist_sgf str s1, int p1

	k = s1
	notesel k

	hit  = 0
	repeat notemax
		noteget t, cnt
		if int(t) == p1 {
			hit = 1
			break
		}
	loop
	
	noteunsel

	return hit
	
#deffunc validate_sgf_file str fname
	sdim sgf, 0
	
	notesel sgf
	noteload fname
	
	; サイズチェック（小さすぎるファイル）
	if strlen(sgf) < 50 {
		goto *delete_file
	}

	; HTML混入チェック（よくあるエラー出力）
	if instr(sgf,0, "<html>") >= 0 or instr(sgf,0, "<!DOCTYPE") >= 0 {
		goto *delete_file
	}

	; SGFの終端 ')' が存在するか
	if instr(sgf,0, ")") < 0 {
		goto *delete_file
	}

	; 最低限のプロパティがあるか（PB[]など）
	if instr(sgf,0, "PB[") < 0 or instr(sgf,0, "PW[") < 0 {
		goto *delete_file
	}

	return 0 ; ←正常

*delete_file
	logmes "SGFファイル破棄: " + fname
	delete fname
	return 1 ; ←破棄したよ
#global



#if 00
	a = "C:\\Users\\owner\\Desktop\\katago-kifu-search\\棋譜"
	chdir a

	//保存先フォルダ名, 最大取得数, アクセス頻度(ms), 最大実行時間(ms)  
	棋譜取得 a        , 10000     , 3000            , 1000 * 60 * 1

#endif 