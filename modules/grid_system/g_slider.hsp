;#define global MODULE_DEBUG
#include "area_grid_system.hsp"
#addition "../keyfps.hsp"
#module grid_slider v, min, max, horizontal, x, y, tickness,長さ, dragging, area, r

#include "rectmock.hsp"
#modinit var a, int _horizontal, int area_number, int _min, int _max, int 太さ, int つまみサイズ, int _x, int _y, local thismod_ID

    dup v, a

	//スライダーの方向
    horizontal = _horizontal
    //エリア番号
    area = area_number

    //値の変化範囲
    min = _min
    max = _max
    //バーのサイズ情報
    tickness = 太さ
    r = つまみサイズ
    
	set_position_g_slider thismod, _x, _y

	//エリア番号から親レクト取得
	thisrect thismod, oya, area
	
	if　horizontal{
		長さ = oya.2 - oya
	}else{
		長さ = oya.3 - oya.1
	}
    mref thismod_ID, 2
    return thismod_ID

#modfunc set_position_g_slider int _x, int _y
	x = _x
	y = _y
	return

#modfunc set_size_g_slider int _sizex, int _sizey, int _sizer
	長さ = _sizex
	tickness = _sizey
	r = _sizer
	return


#modcfunc get_value_g_slider
	return v

#modfunc slider_manager

	thisrect thismod, oya, area

	if horizontal = 1{
		bar_rect = oya + x, oya.1 + y, limit(oya + x + 長さ    ,, oya.2),  limit(oya.1 + y + tickness,,oya.3)
	}else{
		bar_rect = oya + x, oya.1 + y, limit(oya + x + tickness,, oya.2),  limit(oya.1 + y + 長さ    ,,oya.3)
	}
	
	if _getkey(1) == 1{
		if in_rect(bar_rect){
			dragging = 1
		}
	}

	if _getkey(1) == -1{
		dragging = 0
	}

	//ドラッグで値を変更
	if dragging{
		if horizontal{
			v = min + limit((max - min) * (mousex - bar_rect.0 - r ) / (bar_rect.2 - bar_rect.0 - r), 0, max-min)
		}else{
			v = min + limit((max - min) * (mousey - bar_rect.1 - r ) / (bar_rect.3 - bar_rect.1 - r), 0, max-min)
		}
	}


	//値をもとにつまみ位置を計算
	if max != min{
		if horizontal{
			tumami(0) = limit(bar_rect.0 + r/2 + (bar_rect.2 - bar_rect.0 - r) * (v - min) / (max - min), bar_rect.0, bar_rect.2)
			tumami(1) = bar_rect.1 + bar_rect.3 >> 1
		}else{
			tumami(0) = bar_rect + bar_rect.2 >> 1
			tumami(1) = limit(bar_rect.1 + r/2 + (bar_rect.3 - bar_rect.1 - r) * (v - min) / (max - min), bar_rect.1, bar_rect.3)
		}
	}

	#ifdef MODULE_DEBUG
		color 0,255,0:boxf oya,oya.1,oya.2,oya.3 //単体デバッグ用
	#endif
	
	color
	rboxf bar_rect,bar_rect.1,bar_rect.2,bar_rect.3
	color 255
	
	circle tumami - r/2, tumami.1 -r/2, tumami + r/2, tumami.1 + r/2
	
	return
	
#defcfunc local in_rect array a
	return a(0) <= mousex && a(1) <= mousey && a(2) >= mousex && a(3) >= mousey

#global


#ifdef MODULE_DEBUG //テストコード

mock_flag@global = 1

set_rect_mock 99, 100, 100, 200, 300 // モック矩形設定（垂直スライダー用）
set_rect_mock 66, 300, 100, 500, 150 // モック矩形設定（水平スライダー用）
vertical = 0     : newmod a, grid_slider, vertical  , 0, 99, 0   , 300 , 10, 15, 20, 60:
horizontal = 0   : newmod a, grid_slider, horizontal, 1, 66, -100, 300 , 10, 15, 0 , 20 :

//メインループ
repeat
	redraw:redraw 2
	manage_keyfps ;FPS調整と、_getkey()サポート
	
	color 255,255,255:boxf:color
	pos 200,300
	foreach a
		slider_manager a.cnt
		mes get_value_g_slider( a.cnt )
	loop
	
	//連動チェック
	
	if _getkey(2) == 1{ //右クリック
		vertical = rnd(300)//値を直接書き換えてつまみが移動するか？
	}
loop

#endif