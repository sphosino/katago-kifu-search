#ifndef grid_combox

#addition "renderer.hsp"
#addition "color_manager.hsp"
#addition "area_grid_system.hsp"
#addition "id_manager.hsp"

#module grid_combox s, v, item_ysize, item_num, area, dropdown, dropdown_result, closing, drop_id

#include "rectmock.hsp"

#modinit var a, str s1, int area_number, local thismod_ID

    dup a, dropdown_result //　選択アイテムを示す変数

	area = area_number　//エリア番号
	s = s1 //表示文字列

	item_ysize = 30

	dim drop_id, 100

	notesel s
	noteget t, 0
	noteunsel

	thisrect thismod, oya, area
	centerx = (oya.2 + oya.0) / 2
	centery = (oya.3 + oya.1) / 2
	sizex = (oya.2 - oya.0)
	sizey = (oya.3 - oya.1)
	
	combox_id = make_draw_object(MOVE_TYPE_EASE, "STR", LIFE_FOREVER, centerx, centery, sizex, sizey, strf("%s,$000000,メイリオ,0,0,$AAAAAA",t))
	update_priority_draw_object combox_id, PRIORITY_FRONT

    mref thismod_ID, 2
    return thismod_ID

#modcfunc get_value_g_combox
	return dropdown_result

#modfunc combox_manager
	thisrect thismod, oya, area
	notesel s

	if dropdown {
		repeat notemax
			noteget t, cnt
			
			rect_x1 = oya
			rect_y1 = oya.3 + cnt * item_ysize
			rect_x2 = oya.2
			rect_y2 = oya.3 + (cnt + 1) * item_ysize

			center_x = (rect_x1 + rect_x2) / 2
			center_y = (rect_y1 + rect_y2) / 2
			_width = rect_x2 - rect_x1
			height = rect_y2 - rect_y1

			// リスト背景
			color_code = $808080 ; 通常: 128,128,128
			tmprect = rect_x1,rect_y1,rect_x2,rect_y2
			if in_rect(tmprect) {
				color_code = $C8C8C8 ; 選択中: 200,200,200

				update_params_draw_object drop_id(cnt),strf("%s,$000000,メイリオ,0,0,%d",t,color_code)

				if _getkey(1) == 1 {
					dropdown_result = cnt
					update_params_draw_object combox_id, strf("%s,$000000,メイリオ,0,0,%d",t,color_code)
					closing = 1 //閉じ処理中フラグ
				}
			}else{
				update_params_draw_object drop_id(cnt),strf("%s,$000000,メイリオ,0,0,%d",t,color_code)
			}

		loop
	}
	
	if closing {
	    alive = 0
	    repeat notemax
	        if is_entity_id_alive(drop_id(cnt)) : alive = 1 : break
	    loop
	    if alive == 0 {
	        dropdown = 0
	        closing = 0
	    }
	}

	open_time = 0.3
	close_time = 0.3
	
	if _getkey(1) == 1 {
		if dropdown {
			dropdown = 0
			repeat notemax //削除して再生成
				destroy_draw_object_immediately drop_id(cnt)

				noteget t, cnt
				centerx = (oya.2 + oya.0)  / 2
				centery = oya.3 + item_ysize/ 2 + cnt * item_ysize
				sizex = (oya.2 - oya.0)
				sizey = item_ysize
				id = make_draw_object(MOVE_TYPE_EASE, "STR", close_time, centerx, centery, sizex, sizey, strf("%s,$000000,メイリオ,0,0,$AAAAAA",t))
				set_ease_posy get_entity_id(id), centery, oya.3, ease_quartic_inout
				set_ease_sizey get_entity_id(id), sizey, 0, ease_quartic_inout
				update_priority_draw_object id, PRIORITY_FRONT
			loop
			
		} else {
			if in_rect(oya) && closing == 0{

				repeat notemax //各アイテムを生成
					noteget t, cnt
					centerx = (oya.2 + oya.0)  / 2
					centery = oya.3 + item_ysize / 2 + cnt * item_ysize
					sizex = (oya.2 - oya.0)
					sizey = item_ysize
					drop_id(cnt) = make_draw_object(MOVE_TYPE_EASE, "STR", LIFE_FOREVER, centerx,centery,sizex,sizey,strf("%s,$000000,メイリオ,0,0,$AAAAAA",t))
					set_ease_posy get_entity_id(drop_id(cnt)), oya.3,  centery, ease_quartic_inout
					set_ease_sizey get_entity_id(drop_id(cnt)), 0, sizey,ease_quartic_inout
					set_easetime_all get_entity_id(drop_id(cnt)), open_time
					update_priority_draw_object drop_id(cnt), PRIORITY_FRONT
				loop
				dropdown = 1
			}
		}
	}

	noteunsel
	return
	
#defcfunc local in_rect array a
	return a(0) <= mousex && a(1) <= mousey && a(2) >= mousex && a(3) >= mousey

#global
#endif