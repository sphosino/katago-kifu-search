#ifndef treebox
#addition "tree.hsp"
#addition "scrollbar.hsp"
#module treebox root_tree_node, current_node, onmouse_id, area_number, s

#modinit int _area
	area_number = _area
	treebox_clear thismod
	newmod s, scroll
	return

#modfunc set_current_node int p1, local i
	i = p1 != current_node
	current_node = p1
	return i
	
#modcfunc get_current_node
	return current_node

#modcfunc get_root_tree_node
	return root_tree_node

#modfunc treebox_draw  int data_num
	if root_tree_node >= 0{
		get_rect area_number,rect
		set_treeview_endposition rect.2,rect.3
		get_treeview_nodesize t
		set_treeview_position rect.0 + t/2,  rect.1 + t.1/2
		
		tree_view root_tree_node, -1, -1, -1
		onmouse_id = treeview_onmouse_id()

		//スクロールバー
		set_position_scroll_bar s, rect.0, rect.3
		set_direction s, 1 | 2 
		set_size_scroll_bar s, rect.2-rect.0
		set_tickness_scroll_bar s, 10
		set_value_minmax s, 0, data_num
		draw_scroll_bar s, 0, mousex < rect || mousex > rect.2 || mousey < rect.1 || mousey > rect.3 || onmouse_id >= 0

		set_treeview_scroll get_value_scroll_bar(s)
	}

	return

#modcfunc treebox_onmouseid
	return onmouse_id
	
#modfunc treebox_toggle_openflag int p1
	toggle_openflag p1
	return

#modfunc treebox_clear
	current_node = -1
	root_tree_node = -1
	onmouse_id = -1
	set_treeview_scroll
	return root_tree_node

#modfunc make_tree_topnode
	tmp = make_new_SGFnode(-1)
	current_node = make_treenode(-1, tmp)
	logmes "tree"+current_node
	return current_node
	
#modfunc load_from_SGF_topnode int SGF_topnode

	logmes "AAA" + SGF_topnode

	node_stack = SGF_topnode ; 現在のSGFノード番号	
	parent_tree_stack = -1   ; 親ノード番号(SGFノードではなく、ツリーノードの番号）
	sn = 1

	root_tree_node = -1
	
	while sn
		sn--
		
		nnode = node_stack(sn) //SGFノード
		parent_tree_node = parent_tree_stack(sn)//親ツリーノード

		tnode = make_treenode(parent_tree_node, nnode) //親ツリーに新しくアイテムを作成、そのアイテムにSGFノードIDを納める

		if root_tree_node = -1: root_tree_node = tnode
		
		//子ノードを全部積む。
		repeat get_SGF_childs(nnode, childlist)
			node_stack(sn) = lpeek(childlist,cnt*4)
			parent_tree_stack(sn) = tnode
			sn++
		loop

	wend

	logmes "rootnode -> " + root_tree_node

	set_current_node thismod, root_tree_node
	return root_tree_node

#global
#endif