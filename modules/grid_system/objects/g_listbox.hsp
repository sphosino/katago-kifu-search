#include "../hsp_common/id_manager_for_list.hsp"

#const global LISTBOX_DEFAULT_MAX 300

#module grid_listbox listbox_id_manager, item_names, item_datas, scrollbar, area_number, item_y, listbox_item_select_status

#modinit int p1, int p2, local thismod_ID

	area_number = p1
	item_y = 20

	size = p2
	if p2 = 0: size = LISTBOX_DEFAULT_MAX

	sdim item_names,, size
	sdim item_datas,, size
	dim listbox_item_select_status, size
	
	newmod listbox_id_manager, list_id_manager, size

	newmod scrollbar, scroll

	set_direction scrollbar, 2
	set_tickness_scroll_bar scrollbar, 18
	set_color_scroll_bar scrollbar, $707070, $00FFFF

	mref thismod_ID,2
	return thismod_ID


#modfunc set_listbox_item_ysize int p1
	item_y = p1
	return

//追加、削除、取得関連
#modfunc del_item_all
	clear_list_id_manager listbox_id_manager
	dim listbox_item_select_status, get_list_id_length(listbox_id_manager)
	fix_scroll_after_remove thismod
	return
	
#modfunc add_item str s1
    sdim tmp_str
    ss = s1
    notesel ss
    repeat notemax
        noteget tmp_str, cnt
        id = get_new_id_for_list(listbox_id_manager)
        item_names(id) = tmp_str
    loop
    noteunsel
    return id

//リスト番号ベースで操作
#modfunc add_item_data int index, str s1
	item_datas(get_list_id(listbox_id_manager, index)) = s1
	return
#modcfunc get_item_name int index
    return item_names(get_list_id(listbox_id_manager, index))
#modcfunc get_item_data int index
    return item_datas(get_list_id(listbox_id_manager, index))
#modfunc remove_item int index
	delete_by_listnum listbox_id_manager, index
	fix_scroll_after_remove thismod
	return

//IDベース操作
#modfunc add_item_data_by_id int _id, str s1
	item_datas(_id) = s1
	return
#modcfunc get_item_name_by_id int _id
    return item_names(_id)
#modcfunc get_item_data_by_id int _id
    return item_datas(_id)
#modfunc remove_item_by_id int _id
	delete_by_itemid listbox_id_manager, _id
	fix_scroll_after_remove thismod
	return
	
#modfunc remove_selected_items
	// 選択中のやつを全部削除
	repeat get_selected_item_ids(thismod, selected_ids)
		remove_item_by_id thismod, selected_ids(cnt)
	loop
#modfunc remove_selected_items_with_cursor_follow

	// まず選択中IDたちを拾う
	num = get_selected_item_ids(thismod, selected_ids)
	if num == 0 : return ; そもそも何も選ばれてないなら無視

	// 最初に選択されてたIDを仮にベースにする（複数選択の場合）
	base_id = selected_ids(0)
	base_pos = get_list_position(listbox_id_manager, base_id)

	// 選択されてるIDを削除
	repeat num
		remove_item_by_id thismod, selected_ids(cnt)
	loop

	// 削除後のリスト数を取得
	max_num = get_list_ids(listbox_id_manager, idlist)

	// 何もなくなったら何もしない
	if max_num <= 0 : return

	// 削除前の位置に一番近いところを再選択
	new_pos = limit(base_pos, 0, max_num-1)
	new_id = idlist(new_pos)

	// 全選択をリセットして、新しいIDだけ選択する
	repeat length(listbox_item_select_status)
		listbox_item_select_status(cnt) = 0
	loop
	listbox_item_select_status(new_id) = 1

	return

#modfunc listbox_manager int current_flag
	thisrect thismod, rect, area_number

	moved = 0
	// 現在のスクロール位置
	scroll_value = get_value_scroll_bar(scrollbar)

	// 最大個数
	max_num = get_list_count(listbox_id_manager)

	set_position_scroll_bar scrollbar, rect.2, rect.1
	set_value_minmax scrollbar, 0, limit(max_num - (rect.3 - rect.1) / item_y, 0)
	set_size_scroll_bar scrollbar, rect.3 - rect.1

	color 0,0,0
	aboxf rect
	color 255,255,255

	// --- クリック検出 ---
	if _getkey(1) == 1 {
		if mousex >= rect.0 & mousex < rect.2 & mousey >= rect.1 & mousey < rect.3 {
			clicked_index = (mousey - rect.1) / item_y + scroll_value
			if clicked_index < max_num {
				clicked_id = get_list_id(listbox_id_manager, clicked_index)

				if _getkey(17) >=1 { // Ctrl押しながら
					listbox_item_select_status(clicked_id) = 1 - listbox_item_select_status(clicked_id)
				}else{
					// Ctrl押してないなら単独選択
					repeat get_list_ids(listbox_id_manager,ids)
						listbox_item_select_status(ids.cnt) = 0
					loop
					listbox_item_select_status(clicked_id) = 1
				}
			}
		}
	}

	if current_flag{
		if _getkey(38) ==  1 || _getkey(40) == 1{
			offset = ( _getkey(40) == 1 ) * 2 - 1
			move_listbox_index thismod ,offset
			
		}
		if _getkey(17) >= 1 && _getkey('A') == 1{
			repeat get_list_ids(listbox_id_manager,ids)
				listbox_item_select_status(ids.cnt) = 1
			loop	
		}
	}

	font "", item_y

	// --- 表示 ---
	repeat (rect.3 - rect.1) / item_y
		id_index = cnt + scroll_value
		if id_index >= max_num : break

		id = get_list_id(listbox_id_manager, id_index)

		pos rect.0, rect.1 + cnt * item_y

	    if listbox_item_select_status(id) {
	        color 0, 100, 255
	        boxf rect.0, rect.1 + cnt * item_y, rect.2, rect.1 + (cnt + 1) * item_y
	        color 255,255,255
	    }else{
	        color 255,255,255
	    }

		mes item_names(id)
	loop

	if max_num > (rect.3 - rect.1) / item_y {
		draw_scroll_bar scrollbar
	}

	return
#modfunc move_listbox_index int move_value
	
	clicked_index = limit(get_single_selected_index(thismod) + move_value, 0, get_list_count(listbox_id_manager)-1)
	clicked_id = get_list_id(listbox_id_manager, clicked_index)
	
	repeat get_list_ids(listbox_id_manager,ids)
		listbox_item_select_status(ids.cnt) = 0
	loop
	listbox_item_select_status(clicked_id) = 1

	ensure_visible_index thismod, clicked_index

	return clicked_index
#modfunc ensure_visible_index int index
	thisrect thismod, rect, area_number
	visible_lines = (rect.3 - rect.1) / item_y
	current_scroll = get_value_scroll_bar(scrollbar)

	if index < current_scroll {
		move_scroll_index thismod, index
	} else : if index >= current_scroll + visible_lines {
		move_scroll_index thismod, index - visible_lines + 1
	}
	return
//スクロールバー関連
#modfunc set_listbox_scollbar_width int p1
	set_tickness_scroll_bar scrollbar, p1
	return
#modfunc move_scroll_index int p1
	set_value_scroll_bar scrollbar, p1
	return
#modfunc scroll_to_bottom_listbox
	fix_scroll_after_remove thismod
	scroll_to_bottom scrollbar
	return
#modfunc scroll_to_top_listbox
	fix_scroll_after_remove thismod
	scroll_to_top scrollbar
	return
#modfunc scroll_up
	add_value_scroll_bar scrollbar, -1
	return
#modfunc scroll_down
	add_value_scroll_bar scrollbar, 1
	return
	
//スクロール後処理
#modfunc fix_scroll_after_remove

	thisrect thismod, rect, area_number

	max_num = get_list_ids(listbox_id_manager, idlist)

	visible_lines = (rect.3 - rect.1) / item_y

	max_scroll = limit(max_num - visible_lines, 0)

	refresh_scroll_bar scrollbar, 0, max_scroll

	return

//選択中の要素取得
#modcfunc get_selected_item_ids array a
    dim a   // いったん空にする
    n = 0
    repeat get_list_ids(listbox_id_manager,ids)
    	id = ids(cnt)
        if listbox_item_select_status(id) = 1 {
            a(n) = id
            n++
        }
    loop
    return n
#modcfunc get_selected_item_indices array a
    dim a
    n = 0
    repeat get_list_ids(listbox_id_manager, ids)
        id = ids(cnt)
        if listbox_item_select_status(id) == 1 {
            a(n) = cnt //←ここ！IDではなく番号(cnt)を保存！
            n++
        }
    loop
    return n

#modcfunc get_list_item_num
	return get_list_count(listbox_id_manager)

//リストボックスのどこにx,yがあるか？
#modcfunc listbox_on_id int x, int y, local i
	thisrect thismod, rect, area_number

	if rect.0 > x || rect.1 > y || rect.2 < x || rect.3 < y {
		//リストボックス外
		return -1
	}
	
	scrollbar_x = rect.2 - get_scroll_bar_width(scrollbar) ; ←スクロールバーのエリア幅（デフォルト18固定）
	if x >= scrollbar_x {
		//スクロールエリア
		bar_area = bar_area_x, bar_area_y, bar_area_x2, bar_area_y2
		get_scroll_bar_area scrollbar, bar_area
		
		if y >= bar_area.1 && y <= bar_area.3 {
			//つまみの上
			return -2
		}
		return -3
	}

	//アイテムエリア内
	current_scroll_index = get_value_scroll_bar(scrollbar)
	max_num = get_list_ids(listbox_id_manager, idlist)
	i = (y - rect.1) / item_y + current_scroll_index

	if i >= max_num {
		//アイテム外
		return -4
	}

	return i // アイテム番号（0始まり）

#modcfunc get_single_selected_index
	result = -1
	repeat get_list_ids(listbox_id_manager, ids)
		id = ids(cnt)
		if listbox_item_select_status(id) == 1 {
			result = cnt
			break
		}
	loop
	return result



//---util functions

#modcfunc get_name_all_items //非推奨。重すぎ
	s = ""
	repeat get_list_ids(listbox_id_manager, ids)
		id = ids(cnt)
		if s!=""{
			s+= "\n"+ item_names(ids)
		}else{
			s+= item_names(ids)
		}
	loop
	return s

	
#include "rectmock.hsp"
#global

#if 0
mock_flag@global = 1
set_rect_mock 0, 200,100,400,200

newmod a, grid_listbox

// 初期データ
add_item a,"A\n改行"
add_item a,"B"
add_item a,"C"
add_item a,"D"
add_item a,"E"
add_item a,"F"
add_item a,"G"

add_item_data a,2,"データだよ"

*main
	// 毎フレーム更新
	manage_keyfps //_getkey()のサポートとfps管理

	color 50,50,50:boxf

	listbox_manager a

	pos 0,0
	color 255,255,255

	// ★ 全リストを表示して、選択中はマークつける！
	mes "=== リスト内容 ==="
	max_num = get_list_item_num(a)

	repeat max_num
		idx = cnt
		if idx >= 0 {
			itemname = get_item_name(a, idx)
			itemdata = get_item_data(a, idx)

			selected = 0
			repeat get_selected_item_indices(a, idxs)
				if idx == idxs(cnt2) : selected = 1
			loop

			if selected {
				mes "★[" + idx + "] " + itemname + " : " + itemdata
			}else{
				mes " [" + idx + "] " + itemname + " : " + itemdata
			}
		}
	loop

	// ★ 選択数カウントも表示
	num_selected = get_selected_item_indices(a, idxs)
	mes " "
	mes "選択数 : " + num_selected

	// ★ キー操作
	if _getkey(46) == 1 {    // Deleteキー押したら
		remove_selected_items_with_cursor_follow a
	}

	if _getkey(2) == 1{ // 右クリック押したら
		add_item a, "NEW_ITEM"
		scroll_to_bottom_listbox a
	}

	redraw
	redraw 2
goto *main
#endif