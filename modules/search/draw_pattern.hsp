#module
#const BOARD_X_MAX 26
#const BOARD_Y_MAX 26

#deffunc group_draw var board, array group
	get_goban_mas_size board, goban_mas_size
	get_goban_position board, goban_position
	group_str = "A","B","C","D"
	//重複グループ地点を計算
	dim group_board,BOARD_X_MAX,BOARD_Y_MAX
	for i,,4
		repeat get_coordinates_set_int(group(i),t)
			x = t(0, cnt)
			y = t(1, cnt)
			group_board(x,y)++
		loop
	next

	for i,, 4
	repeat get_coordinates_set_int(group(i),t)
	 	
		x = t(0, cnt)
		y = t(1, cnt)
		text_color = select_group_color(get_goban_position_color(board,x,y))
		sx = goban_mas_size
		sy = goban_mas_size.1
		cx = goban_position   + x * goban_mas_size
		cy = goban_position.1 + y * goban_mas_size.1
		if group_board(x,y) >= 1 {
			sx /= 2
			sy /= 2
			cx += (i \ 2) * sx + sx / 2
			cy += (i / 2) * sy + sy / 2
		}else{
			cx += sx / 2
			cy += sy / 2
		}
		make_instant_draw_object "STR", cx, cy, sx, sy, strf("%s,%d", group_str(i), text_color)
	loop
	next
	return
#deffunc tejun_draw var board, array tejun
	get_goban_mas_size board, goban_mas_size
	get_goban_position board, goban_position
	for i,,9
	repeat get_coordinates_set_int(tejun(i),t)
		
		x = t(0, cnt)
		y = t(1, cnt)
		text_color = select_tejun_color(get_goban_position_color(board,x,y))
		sx = goban_mas_size
		sy = goban_mas_size.1
		cx = goban_position   + x * goban_mas_size
		cy = goban_position.1 + y * goban_mas_size.1
		cx += sx / 2
		cy += sy / 2
		
		make_instant_draw_object "STR", cx, cy, sx, sy, strf("%s,%s", str(i + 1), text_color)
	loop
	next
	return


#defcfunc select_group_color int c
	switch c
	case 0
	case 2
	case 3
	case 5
	case 6 : return "$000000" ; 黒文字
	case 1
	case 4 : return "$FFFFFF" ; 白文字
	default : dialog "想定外の石の種類です",,__FILE__: end
	swend
#defcfunc select_tejun_color int c
	switch c
	case 0
	case 2
	case 3
	case 5
	case 6 : return "$403040" ; 黒文字
	case 1
	case 4 : return "$A85898" ; 白文字
	default : dialog "想定外の石の種類です",,__FILE__: end
	swend
#global