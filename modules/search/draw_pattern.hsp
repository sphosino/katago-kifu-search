#ifndef init_draw_pattern
#module
#const BOARD_X_MAX 26
#const BOARD_Y_MAX 26

#deffunc debug_tg_draw var board, array group, array tejun
	if rnd(10): return
	get_goban_position board, goban_position
	get_goban_mas_size board, goban_mas_size
	
	// 具体的な値を確認
	logmes strf("goban_position: %d", goban_position)
	logmes strf("goban_mas_size: %d", goban_mas_size)

	// 左端、中央、右端の計算結果
	x = 0
	cx0 = goban_position + x * goban_mas_size + goban_mas_size / 2
	logmes strf("x=0: cx=%d", cx0)
	
	x = 9  
	cx9 = goban_position + x * goban_mas_size + goban_mas_size / 2
	logmes strf("x=9: cx=%d", cx9)
	
	x = 18
	cx18 = goban_position + x * goban_mas_size + goban_mas_size / 2
	logmes strf("x=18: cx=%d", cx18)
	
	// 理論値との比較
	logmes strf("理論的な間隔: %d", goban_mas_size)
	logmes strf("実際の間隔 0-9: %d", cx9 - cx0)
	logmes strf("実際の間隔 9-18: %d", cx18 - cx9)

	return

#deffunc group_tejun_draw var board, array group, array tejun
	get_goban_mas_size board, goban_mas_size
	get_goban_position board, goban_position
	
	//各地点を計算
	dim tmp_board, BOARD_X_MAX, BOARD_Y_MAX
	for i,,4
		repeat get_coordinates_set_int(group(i),t)
			x = t(0, cnt)
			y = t(1, cnt)
			tmp_board(x,y)++
		loop
	next
	for i,,9
		repeat get_coordinates_set_int(tejun(i),t)
			x = t(0, cnt)
			y = t(1, cnt)
			tmp_board(x,y) = 2
		loop
	next

	for i,, 4
	repeat get_coordinates_set_int(group(i),t)
	 	
		x = t(0, cnt)
		y = t(1, cnt)
		text_color = select_group_color(get_goban_position_color(board,x,y))
		sx = goban_mas_size
		sy = goban_mas_size.1
		cx = goban_position   + x * sx
		cy = goban_position.1 + y * sy
		if tmp_board(x,y) >= 2 {
			sx /= 2
			sy /= 2
			cx += (i \ 2) * (sx + 1)/2 + (sx + 1) / 2
			cy += (i / 2) * (sy + 1)/2 + (sy + 1) / 2
		}else{
			cx += sx / 2
			cy += sy / 2
		}
		make_instant_draw_object "STR", cx, cy, sx, sy, strf("%s,%d", group_str(i), text_color)
	loop
	next

	//手順描画
	for i,,9
	repeat get_coordinates_set_int(tejun(i),t)
		
		x = t(0, cnt)
		y = t(1, cnt)
		text_color = select_tejun_color(get_goban_position_color(board,x,y))
		sx = goban_mas_size
		sy = goban_mas_size.1
		cx = goban_position   + x * sx
		cy = goban_position.1 + y * sy
		cx += sx / 2
		cy += sy / 2
		
		make_instant_draw_object "STR", cx, cy, sx, sy, strf("%s,%s", str(i + 1), text_color)
	loop
	next
	return


#defcfunc select_group_color int c
	switch c
	case 0
	case 2
	case 3
	case 5
	case 6 : return "$000000" ; 黒文字
	case 1
	case 4 : return "$FFFFFF" ; 白文字
	default : dialog "想定外の石の種類です",,__FILE__: end
	swend
#defcfunc select_tejun_color int c
	switch c
	case 0
	case 2
	case 3
	case 5
	case 6 : return "$403040" ; 黒文字
	case 1
	case 4 : return "$A85898" ; 白文字
	default : dialog "想定外の石の種類です",,__FILE__: end
	swend
#deffunc init_draw_pattern
	group_str = "A","B","C","D"
	return
#global
init_draw_pattern
#endif